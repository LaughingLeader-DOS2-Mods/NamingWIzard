Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderUpdater_Register_Mod("NamingWizard", "LaughingLeader", "0.9.0.0");
LeaderUpdater_Register_ActiveGoal("NamingWizard", "LaughingLeader", "LLNWIZ_ZZZ_Updater");
KBSECTION
//REGION MOD_UPDATING
PROC
LLNWIZ_System_UpdateDatabases()
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater] Databases updated.");

PROC
LLNWIZ_System_RegisterDatabases()
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater] Databases registered.");

PROC
LLNWIZ_Updater_ResetDatabases()
THEN
LeaderUpdater_Clear_AllDatabases("NamingWizard", "LaughingLeader");
LLNWIZ_System_UpdateDatabases();
LeaderLog_Log("DEBUG", "[LLNWIZ:Updater] Databases cleared and re-initialized.");
//END_REGION

//REGION UPDATE_EVENT
IF
GameStarted(_,_)
AND
LeaderUpdater_QRY_ModUpdateNeeded("NamingWizard", "LaughingLeader", "0.9.0.0")
THEN
LeaderUpdater_Register_Mod("NamingWizard", "LaughingLeader", "0.9.0.0");
LLNWIZ_System_UpdateDatabases();
//END_REGION

//REGION UNINSTALLING
PROC
LLNWIZ_Updater_UninstallMod()
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Uninstalling Naming Wizard.");
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Attempting to end all Naming Wizard dialogs. Resuming in 2 seconds.");
LLNWIZ_Updater_CloseAllNamingWizardDialogs();
TimerLaunch("LLNWIZ_Timers_Updater_UninstallerPhase2", 2000);

IF
TimerFinished("LLNWIZ_Timers_Updater_UninstallerPhase2")
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Clearing all flags.");
LLNWIZ_Updater_ClearFlags();
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Destroying all Naming Wizard items.");
LLNWIZ_Updater_DestroyNamingWizardItems();
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Destroying active wizards.");
LLNWIZ_Wizard_DeleteAllWizards();
LeaderLog_Log("DEBUG", "[LLLIB:Updater:Uninstaller] Clearing player variables.");
LLLNIW_Updater_ClearPlayerVariables();
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Clearing databases.");
LeaderUpdater_Clear_AllDatabases("NamingWizard", "LaughingLeader");
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Unregistering Naming Wizard.");
LeaderUpdater_Clear_AllRegisteredData("NamingWizard", "LaughingLeader");
GlobalClearFlag("NamingWizard_Active");

PROC
LLNWIZ_Updater_CloseAllNamingWizardDialogs()
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
DialogRequestStop(_Player);

PROC
LLNWIZ_Updater_ClearFlags()
THEN
LLNWIZ_Settings_ClearGlobalFlags();

PROC
LLNWIZ_Updater_ClearFlags()
AND
DB_IsPlayer(_Player)
THEN
LLNWIZ_Settings_ClearObjectFlags(_Player);
LLNWIZ_Settings_ClearUserFlags(_Player);

PROC
LLNWIZ_Updater_UnequipNamingWizardItem((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
ItemIsEquipable(_Item, 1)
THEN
CharacterUnequipItem(_Player, _Item);

PROC
LLNWIZ_Updater_DestroyNamingWizardItems()
AND
DB_IsPlayer(_Player)
AND
DB_LLNWIZ_ItemTemplates(_Name, _Template)
AND
GetItemForItemTemplateInInventory(_Player, _Template, _Item)
THEN
LLNWIZ_Updater_UnequipNamingWizardItem(_Player, _Item);
ItemDestroy(_Item);
//END_REGION

//REGION ACTIVE_FLAG
IF
GameStarted(_,_)
AND
GlobalGetFlag("NamingWizard_Active", 0)
THEN
GlobalSetFlag("NamingWizard_Active");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
