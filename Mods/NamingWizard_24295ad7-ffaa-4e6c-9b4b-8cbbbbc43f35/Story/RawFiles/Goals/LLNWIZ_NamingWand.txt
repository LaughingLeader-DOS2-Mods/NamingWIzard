Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLNWIZ_NamingWand_InitializeSettings();

//DB_LLNWIZ_NamingWand_Skills(_Skill)
//DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
//DB_LLNWIZ_NamingWand_TimerDelay(_SkillUseDelay, _CastDelay)
//DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
KBSECTION
//REGION SETTINGS
PROC
LLNWIZ_NamingWand_InitializeSettings()
THEN
DB_LLNWIZ_NamingWand_Skills("Target_LLNWIZ_Rename");
DB_LLNWIZ_NamingWand_TimerDelay(3000, 700);
//END_REGION

//REGION PERMISSIONS
QRY
LLNWIZ_QRY_NamingWand_CanRename((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
CharacterIsPlayer(_Target, 1)
AND
LLNWIZ_QRY_UserMatch(_Player, _Target)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_NamingWand_CanRename((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
CharacterIsPlayer(_Target, 1)
AND
NOT LLNWIZ_QRY_UserMatch(_Player, _Target)
AND
GlobalGetFlag("LLNWIZ_Settings_CrossPlayerRenamingEnabled", 1)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_NamingWand_CanRename((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
CharacterIsPlayer(_Target, 0)
THEN
DB_NOOP(1);
//END_REGION

PROC
LLNWIZ_NamingWand_ClearPreviousTargets((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
THEN
LLNWIZ_NamingWand_RemoveBlockTag(_Target);
NOT DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill);

PROC
LLNWIZ_NamingWand_CreateFailTimer((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
GetUUID(_Player, _ID)
AND
StringConcatenate("LLNWIZ_Timers_RenamingFailedTimer_", _ID, _TimerName)
THEN
TimerLaunch(_TimerName, 4000);
DB_LLNWIZ_NamingWand_FailTimer(_TimerName, _Player, _Target);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_NamingWand_FailTimer(_TimerName, _Player, _Target)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_RenamingFailed");
LLNWIZ_Wizard_HideWizardByTarget(_Target);
NOT DB_LLNWIZ_NamingWand_FailTimer(_TimerName, _Player, _Target);

//REGION SKILL_USE
IF
CharacterUsedSkillOnTarget(_Player, (CHARACTERGUID)_Target, _Skill, _, _)
AND
DB_LLNWIZ_NamingWand_Skills(_Skill)
AND
GlobalGetFlag("LLNWIZ_Settings_RenameWandDisabled", 1)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_RenamingDisabled");

IF
CharacterUsedSkillOnTarget(_Player, (CHARACTERGUID)_Target, _Skill, _, _)
AND
DB_LLNWIZ_NamingWand_Skills(_Skill)
AND
GlobalGetFlag("LLNWIZ_Settings_RenameWandDisabled", 0)
AND
NOT LLNWIZ_QRY_NamingWand_CanRename(_Player, _Target)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_UnableToRename");
CharacterStatusText(_Target, "LLNWIZ_StatusText_RenamingResisted");

IF
CharacterUsedSkillOnTarget(_Player, (CHARACTERGUID)_Target, _Skill, _, _)
AND
DB_LLNWIZ_NamingWand_Skills(_Skill)
AND
GlobalGetFlag("LLNWIZ_Settings_RenameWandDisabled", 0)
AND
LLNWIZ_QRY_NamingWand_CanRename(_Player, _Target)
AND
GetUUID(_Player, _ID)
AND
StringConcatenate("LLNWIZ_Timers_RenameTarget_", _ID, _TimerName)
AND
DB_LLNWIZ_NamingWand_TimerDelay(_SkillUseDelay, _CastDelay)
THEN
LLNWIZ_NamingWand_ClearPreviousTargets(_Player);
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill);
LLNWIZ_NamingWand_SetBlockTag(_Player);
LLNWIZ_NamingWand_SetBlockTag(_Target);
TimerLaunch(_TimerName, _SkillUseDelay); // Failsafe, in case the skill never reaches a "cast"
ProcObjectTimer(_Player, "LLNWIZ_Timers_BlockTagFailsafe", 5000); // Failsafe for the block tag not being cleared

//In case the dialog never started
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player, "LLNWIZ_Timers_BlockTagFailsafe")
AND
IsTagged(_Player, "NAMINGWIZARD_BLOCK_RENAMING", 1)
AND
NOT DB_DialogNPCs(_, _Player, _)
AND
NOT DB_DialogPlayers(_, _Player, _)
THEN
LLNWIZ_NamingWand_ClearPreviousTargets(_Player);
LLNWIZ_NamingWand_RemoveBlockTag(_Player);

IF
SkillCast(_Player, _Skill, _, _)
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
AND
DB_LLNWIZ_NamingWand_TimerDelay(_SkillUseDelay, _CastDelay)
THEN
ApplyStatus(_Target, "LLNWIZ_RENAMING", 999.0, 1, _Player);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _CastDelay);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
THEN
LLNWIZ_Wizard_Start(_Player, _Target);

//END_REGION

IF
StoryEvent((CHARACTERGUID)_Player, "LLNWIZ_Events_NamingComplete")
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
THEN
LLNWIZ_Naming_RenameCharacter(_Player, _Target);
LLNWIZ_Naming_ClearSessionVariables(_Player);
NOT DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill);
LLNWIZ_NamingWand_RemoveBlockTag(_Player);
LLNWIZ_NamingWand_RemoveBlockTag(_Target);

IF
StoryEvent((CHARACTERGUID)_Player, "LLNWIZ_Events_NamingCanceled")
THEN
LLNWIZ_NamingWand_ClearPreviousTargets(_Player);
LLNWIZ_NamingWand_RemoveBlockTag(_Player);

//REGION TAGGING
PROC
LLNWIZ_NamingWand_SetBlockTag((CHARACTERGUID)_Target)
AND
IsTagged(_Target, "NAMINGWIZARD_BLOCK_RENAMING", 0)
THEN
SetTag(_Target, "NAMINGWIZARD_BLOCK_RENAMING");
DB_LLNWIZ_NamingWand_TaggedTarget(_Target);

// Only remove the block tag if it was set by these procs originally
PROC
LLNWIZ_NamingWand_RemoveBlockTag((CHARACTERGUID)_Target)
AND
DB_LLNWIZ_NamingWand_TaggedTarget(_Target)
THEN
ClearTag(_Target, "NAMINGWIZARD_BLOCK_RENAMING");
NOT DB_LLNWIZ_NamingWand_TaggedTarget(_Target);
//END_REGION

//REGION UPDATES
PROC
LeaderUpdater_ModUpdated("NamingWizard", "LaughingLeader", _OldVersion, "1.0.1.0")
THEN
LeaderLib_SendEvent("LLNWIZ_Commands_StartBlockTagClearIterator");

IF
GlobalFlagSet("LLNWIZ_Commands_RunBlockedTagSanityIterator")
THEN
GlobalClearFlag("LLNWIZ_Commands_RunBlockedTagSanityIterator");
LeaderLib_SendEvent("LLNWIZ_Commands_StartBlockTagClearIterator");

IF
StoryEvent((CHARACTERGUID)_Character, "LLNWIZ_Commands_BlockTagSanityCheck")
AND
IsTagged(_Character, "NAMINGWIZARD_BLOCK_RENAMING", 1)
AND
NOT DB_DialogNPCs(_, _Character, _)
AND
NOT DB_DialogPlayers(_, _Character, _)
THEN
LLNWIZ_NamingWand_RemoveBlockTag(_Character);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
