Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLNWIZ_Naming_Words_InitializeSettings();
//Settings
//DB_LLNWIZ_Naming_CommonWords(_Index, _Word)
//DB_LLNWIZ_Naming_PlayerWords(_Index, _Word)
//DB_LLNWIZ_Naming_ModWords(_Index, _Word)
//DB_LLNWIZ_Naming_DialogVariables(_Shortname, _VariableName)
//DB_LLNWIZ_Naming_WordVariables(_WordIndex, _DialogVar, _SelectFlag, _DisabledFlag)
//DB_LLNWIZ_Naming_WordPageSettings(_WordsPerPage)
//Temp variables
//DB_LLNWIZ_Temp_WordVariableValue(_Index, _Word)
//Variables
//DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
//DB_LLNWIZ_Naming_LastWord(_Player, _LastWord)
//DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
KBSECTION
//REGION SETTINGS_WORDS
PROC
LLNWIZ_Naming_Words_AddWord((STRING)_Word)
AND
NOT DB_LLNWIZ_Naming_CommonWords(_,_)
THEN
DB_LLNWIZ_Naming_CommonWords(0, _Word);

PROC
LLNWIZ_Naming_Words_AddWord((STRING)_Word)
AND
NOT DB_LLNWIZ_Naming_CommonWords(_,_Word)
AND
SysCount("DB_LLNWIZ_Naming_CommonWords", 2, _Index)
THEN
DB_LLNWIZ_Naming_CommonWords(_Index, _Word);

PROC
LLNWIZ_Naming_Words_AddPlayerWord((STRING)_Word)
AND
NOT DB_LLNWIZ_Naming_PlayerWords(_,_Word)
AND
SysCount("DB_LLNWIZ_Naming_PlayerWords", 2, _Count)
AND
IntegerSubtract(_Count, 1, _Index)
THEN
DB_LLNWIZ_Naming_PlayerWords(_Index, _Word);

PROC
LLNWIZ_Naming_Words_AddModWord((STRING)_Word)
AND
NOT DB_LLNWIZ_Naming_ModWords(_,_Word)
AND
SysCount("DB_LLNWIZ_Naming_ModWords", 2, _Count)
AND
IntegerSubtract(_Count, 1, _Index)
THEN
DB_LLNWIZ_Naming_ModWords(_Index, _Word);

PROC
LLNWIZ_Naming_Words_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_CommonWords", 2);
LLNWIZ_Naming_Words_AddWord("Aerothurge");
LLNWIZ_Naming_Words_AddWord("Air");
LLNWIZ_Naming_Words_AddWord("AoE");
LLNWIZ_Naming_Words_AddWord("Armor");
LLNWIZ_Naming_Words_AddWord("Axe");
LLNWIZ_Naming_Words_AddWord("Barter");
LLNWIZ_Naming_Words_AddWord("Battle");
LLNWIZ_Naming_Words_AddWord("Beast");
LLNWIZ_Naming_Words_AddWord("Bow");
LLNWIZ_Naming_Words_AddWord("Charisma");
LLNWIZ_Naming_Words_AddWord("Class");
LLNWIZ_Naming_Words_AddWord("Cloth");
LLNWIZ_Naming_Words_AddWord("Combat");
LLNWIZ_Naming_Words_AddWord("Common");
LLNWIZ_Naming_Words_AddWord("Crafting");
LLNWIZ_Naming_Words_AddWord("Crossbow");
LLNWIZ_Naming_Words_AddWord("Custom");
LLNWIZ_Naming_Words_AddWord("DPS");
LLNWIZ_Naming_Words_AddWord("DualWielding");
LLNWIZ_Naming_Words_AddWord("Dwarven");
LLNWIZ_Naming_Words_AddWord("Earth");
LLNWIZ_Naming_Words_AddWord("Elven");
LLNWIZ_Naming_Words_AddWord("Epic");
LLNWIZ_Naming_Words_AddWord("Fane");
LLNWIZ_Naming_Words_AddWord("Fire");
LLNWIZ_Naming_Words_AddWord("Geomancer");
LLNWIZ_Naming_Words_AddWord("Glass Cannon");
LLNWIZ_Naming_Words_AddWord("Healer");
LLNWIZ_Naming_Words_AddWord("Human");
LLNWIZ_Naming_Words_AddWord("Huntsman");
LLNWIZ_Naming_Words_AddWord("Hydrosophist");
LLNWIZ_Naming_Words_AddWord("Ifan");
LLNWIZ_Naming_Words_AddWord("Junk");
LLNWIZ_Naming_Words_AddWord("Knight");
LLNWIZ_Naming_Words_AddWord("Leader");
LLNWIZ_Naming_Words_AddWord("Leadership");
LLNWIZ_Naming_Words_AddWord("Leather");
LLNWIZ_Naming_Words_AddWord("Legendary");
LLNWIZ_Naming_Words_AddWord("Lifesteal");
LLNWIZ_Naming_Words_AddWord("Lizard");
LLNWIZ_Naming_Words_AddWord("Lockpicking");
LLNWIZ_Naming_Words_AddWord("Lohse");
LLNWIZ_Naming_Words_AddWord("Lone Wolf");
LLNWIZ_Naming_Words_AddWord("Loremaster");
LLNWIZ_Naming_Words_AddWord("Luck");
LLNWIZ_Naming_Words_AddWord("Mace");
LLNWIZ_Naming_Words_AddWord("Mage");
LLNWIZ_Naming_Words_AddWord("Magic");
LLNWIZ_Naming_Words_AddWord("Metal");
LLNWIZ_Naming_Words_AddWord("Mod");
LLNWIZ_Naming_Words_AddWord("Necromancy");
LLNWIZ_Naming_Words_AddWord("Paladin");
LLNWIZ_Naming_Words_AddWord("Perseverance");
LLNWIZ_Naming_Words_AddWord("Persuasion");
LLNWIZ_Naming_Words_AddWord("Physical");
LLNWIZ_Naming_Words_AddWord("Pickpocket");
LLNWIZ_Naming_Words_AddWord("Polymorph");
LLNWIZ_Naming_Words_AddWord("Priest");
LLNWIZ_Naming_Words_AddWord("Pyrokinetic");
LLNWIZ_Naming_Words_AddWord("Ranged");
LLNWIZ_Naming_Words_AddWord("Ranger");
LLNWIZ_Naming_Words_AddWord("Rare");
LLNWIZ_Naming_Words_AddWord("Retribution");
LLNWIZ_Naming_Words_AddWord("Rogue");
LLNWIZ_Naming_Words_AddWord("Rune");
LLNWIZ_Naming_Words_AddWord("Running");
LLNWIZ_Naming_Words_AddWord("Scoundrel");
LLNWIZ_Naming_Words_AddWord("Sebille");
LLNWIZ_Naming_Words_AddWord("Shield");
LLNWIZ_Naming_Words_AddWord("SingleHanded");
LLNWIZ_Naming_Words_AddWord("Sneaking");
LLNWIZ_Naming_Words_AddWord("Source");
LLNWIZ_Naming_Words_AddWord("Spear");
LLNWIZ_Naming_Words_AddWord("Speed");
LLNWIZ_Naming_Words_AddWord("Staff");
LLNWIZ_Naming_Words_AddWord("Stealth");
LLNWIZ_Naming_Words_AddWord("Summoning");
LLNWIZ_Naming_Words_AddWord("Sword");
LLNWIZ_Naming_Words_AddWord("Tank");
LLNWIZ_Naming_Words_AddWord("Telekinesis");
LLNWIZ_Naming_Words_AddWord("Thievery");
LLNWIZ_Naming_Words_AddWord("Town");
LLNWIZ_Naming_Words_AddWord("TwoHanded");
LLNWIZ_Naming_Words_AddWord("Uncommon");
LLNWIZ_Naming_Words_AddWord("Undead");
LLNWIZ_Naming_Words_AddWord("Unique");
LLNWIZ_Naming_Words_AddWord("Vitality");
LLNWIZ_Naming_Words_AddWord("Wand");
LLNWIZ_Naming_Words_AddWord("Warfare");
LLNWIZ_Naming_Words_AddWord("Water");
//END_REGION

//REGION SETTINGS
PROC
LLNWIZ_Naming_Words_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_WordPageSettings", 1);
DB_LLNWIZ_Naming_WordPageSettings(9);

LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_WordVariables", 4);
DB_LLNWIZ_Naming_WordVariables(0, "LLNWIZ_Naming_Word1_e05055e1-d008-44c0-b6d2-f64d11ecf563", "LLNWIZ_Naming_SelectedWord1", "LLNWIZ_Naming_Word1Empty");
DB_LLNWIZ_Naming_WordVariables(1, "LLNWIZ_Naming_Word2_1db77bd2-f3a4-40c7-8a15-f0e0caf5ec6b", "LLNWIZ_Naming_SelectedWord2", "LLNWIZ_Naming_Word2Empty");
DB_LLNWIZ_Naming_WordVariables(2, "LLNWIZ_Naming_Word3_56f0192b-d33c-495a-a5fe-14fd72e3c766", "LLNWIZ_Naming_SelectedWord3", "LLNWIZ_Naming_Word3Empty");
DB_LLNWIZ_Naming_WordVariables(3, "LLNWIZ_Naming_Word4_2459bfdd-2c5f-4c1d-9f54-6b72ce2adde3", "LLNWIZ_Naming_SelectedWord4", "LLNWIZ_Naming_Word4Empty");
DB_LLNWIZ_Naming_WordVariables(4, "LLNWIZ_Naming_Word5_bd2f1c32-7ce3-4cfa-b6cd-f57632ac534c", "LLNWIZ_Naming_SelectedWord5", "LLNWIZ_Naming_Word5Empty");
DB_LLNWIZ_Naming_WordVariables(5, "LLNWIZ_Naming_Word6_c592657e-f33a-4759-bf1f-4be7c88ecc9f", "LLNWIZ_Naming_SelectedWord6", "LLNWIZ_Naming_Word6Empty");
DB_LLNWIZ_Naming_WordVariables(6, "LLNWIZ_Naming_Word7_c1daa983-8936-44da-9dcf-cf734026e32d", "LLNWIZ_Naming_SelectedWord7", "LLNWIZ_Naming_Word7Empty");
DB_LLNWIZ_Naming_WordVariables(7, "LLNWIZ_Naming_Word8_f73547bc-c893-4810-8ad9-9827ac2a2ea0", "LLNWIZ_Naming_SelectedWord8", "LLNWIZ_Naming_Word8Empty");
DB_LLNWIZ_Naming_WordVariables(8, "LLNWIZ_Naming_Word9_1521ba0f-dd1a-4f20-8a67-eee79b9ca96d", "LLNWIZ_Naming_SelectedWord9", "LLNWIZ_Naming_Word9Empty");

PROC
LLNWIZ_System_UpdateDatabases()
THEN
LLNWIZ_Naming_Words_InitializeSettings();

PROC
LLNWIZ_System_ClearDatabases()
THEN
SysClear("DB_LLNWIZ_Naming_CommonWords", 2);
SysClear("DB_LLNWIZ_Naming_WordVariables", 4);
SysClear("DB_LLNWIZ_Naming_WordPageSettings", 1);
SysClear("DB_LLNWIZ_Naming_MaxWordPage", 3);
SysClear("DB_LLNWIZ_Naming_LastWord", 2);
SysClear("DB_LLNWIZ_Naming_CurrentWordPage", 2);
SysClear("DB_LLNWIZ_Temp_WordVariableValue", 2);

LLNWIZ_DebugLog("[Naming_Words] Databases cleared.");
//END_REGION

//REGION VARIABLE_INITIALIZATION
PROC
LLNWIZ_Naming_Words_CalculateMaxPage()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_MaxWordPage", 3);

PROC
LLNWIZ_Naming_Words_CalculateMaxPage()
AND
SysCount("DB_LLNWIZ_Naming_CommonWords", 2, _TotalWords)
AND
DB_LLNWIZ_Naming_WordPageSettings(_WordsPerPage)
AND
IntegerSubtract(_WordsPerPage, 1, _Variance)
AND
IntegerSum(_TotalWords, _Variance, _PageNumToDivide)
AND
IntegerDivide(_PageNumToDivide, _WordsPerPage, _MaxPage)
AND
IntegerSubtract(_TotalWords, 1, _LastIndex)
AND
IntegerSubtract(_MaxPage, 1, _LastPageIndex)
THEN
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage);

IF
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
AND
IntegertoString(_LastIndex, _LastIndexStr)
AND
IntegertoString(_MaxPage, _MaxPageStr)
AND
IntegertoString(_LastPageIndex, _LastPageIndexStr)
AND
StringConcatenate("[Naming_Words] Page values calculated: MaxPage:[", _MaxPageStr, _Str1)
AND
StringConcatenate(_Str1, "] LastWordIndex[", _Str2)
AND
StringConcatenate(_Str2, _LastIndexStr, _Str3)
AND
StringConcatenate(_Str3, "] LastPageIndex[", _Str4)
AND
StringConcatenate(_Str4, _LastPageIndexStr, _Str5)
AND
StringConcatenate(_Str5, "]", _Str6)
THEN
LLNWIZ_DebugLog(_Str6);

PROC
LLNWIZ_Naming_Words_CalculateMaxPage()
THEN
LLNWIZ_System_Debug_DatabaseCount("DB_LLNWIZ_Naming_CommonWords", 2);
LLNWIZ_System_Debug_DatabaseCount("DB_LLNWIZ_Naming_WordVariables", 4);

PROC
LLNWIZ_Naming_Words_InitializeSettings()
THEN
LLNWIZ_Naming_Words_CalculateMaxPage();
//END_REGION

//REGION CLEAR
PROC
LLNWIZ_Naming_Words_ResetPlayerPage((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
THEN
NOT DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page);
DB_LLNWIZ_Naming_CurrentWordPage(_Player, 0);

PROC
LLNWIZ_Naming_Words_ResetPlayerPage((CHARACTERGUID)_Player)
AND
NOT DB_LLNWIZ_Naming_CurrentWordPage(_Player, _)
THEN
DB_LLNWIZ_Naming_CurrentWordPage(_Player, 0);
//END_REGION

//REGION PAGE_TURNING
PROC
LLNWIZ_Naming_Words_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _CurrentPage)
THEN
NOT DB_LLNWIZ_Naming_CurrentWordPage(_Player, _CurrentPage);

PROC
LLNWIZ_Naming_Words_SetPage((CHARACTERGUID)_Player, (INTEGER)_Page, (INTEGER)_Instance)
THEN
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page);

PROC
LLNWIZ_Naming_Words_NextPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
AND
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page < _MaxPage
AND
IntegerSum(_Page, 1, _NextPage)
THEN
LLNWIZ_Naming_Words_SetPage(_Player, _NextPage, _Instance);

PROC
LLNWIZ_Naming_Words_NextPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
AND
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page >= _MaxPage
THEN
LLNWIZ_Naming_Words_SetPage(_Player, 0, _Instance);

PROC
LLNWIZ_Naming_Words_PreviousPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
AND
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page > 0
AND
IntegerSubtract(_Page, 1, _NextPage)
THEN
LLNWIZ_Naming_Words_SetPage(_Player, _NextPage, _Instance);

PROC
LLNWIZ_Naming_Words_PreviousPage((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
AND
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
AND
_Page < 0
THEN
LLNWIZ_Naming_Words_SetPage(_Player, _LastPageIndex, _Instance);
//END_REGION

//REGION PAGE_VARIABLES
PROC
LLNWIZ_Naming_Words_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_CurrentWordPage(_Player, _)
THEN
DB_LLNWIZ_Naming_CurrentWordPage(_Player, 0);
LLNWIZ_DebugLog("[Naming_Words][ERROR] Page not set for some reason. Setting to page 0.");

PROC
LLNWIZ_Naming_Words_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_MaxWordPage(_,_,_)
THEN
LLNWIZ_DebugLog("[Naming_Words][ERROR] Max page settings not set. Reconfiguring.");
LLNWIZ_Naming_Words_CalculateMaxPage();

PROC
LLNWIZ_Naming_Words_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Temp_WordVariableValue(_WordIndex, _Word)
THEN
NOT DB_LLNWIZ_Temp_WordVariableValue(_WordIndex, _Word);

PROC
LLNWIZ_Naming_Words_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
AND
DB_LLNWIZ_Naming_WordPageSettings(_WordsPerPage)
AND
IntegerProduct(_Page, _WordsPerPage, _StartIndex)
AND
DB_LLNWIZ_Naming_WordVariables(_WordIndex, _DialogVar, _SelectFlag, _DisabledFlag)
AND
IntegerSum(_WordIndex, _StartIndex, _Index)
AND
IntegertoString(_Index, _IndexStr)
AND
IntegertoString(_WordIndex, _WordIndexStr)
AND
LLNWIZ_QRY_Naming_DisableWordIfNotAvailable(_Player, _Index, _DisabledFlag)
AND
DB_LLNWIZ_Naming_CommonWords(_Index, _Word)
AND
NOT DB_LLNWIZ_Temp_WordVariableValue(_WordIndex, _) // Failsafe
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _Word);
DB_LLNWIZ_Temp_WordVariableValue(_WordIndex, _Word);
LLNWIZ_DebugLog("Word[",_WordIndexStr,"|",_IndexStr,"] = ",_Word," | Var = ", _DialogVar);

PROC
LLNWIZ_Naming_Words_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
AND
DB_LLNWIZ_Naming_DialogVariables("LastPage", _VariableName)
THEN
DialogSetVariableIntForInstance(_Instance, _VariableName, _MaxPage);

PROC
LLNWIZ_Naming_Words_LoadPageVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentWordPage(_Player, _Page)
AND
IntegerSum(_Page, 1, _DisplayedPage)
AND
DB_LLNWIZ_Naming_DialogVariables("CurrentPage", _VariableName)
THEN
DialogSetVariableIntForInstance(_Instance, _VariableName, _DisplayedPage);

QRY
LLNWIZ_QRY_Naming_DisableWordIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
NOT DB_LLNWIZ_Naming_CommonWords(_Index, _)
AND
ObjectGetFlag(_Player, _DisabledFlag, 0)
THEN
ObjectSetFlag(_Player, _DisabledFlag);

QRY
LLNWIZ_QRY_Naming_DisableWordIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
AND
DB_LLNWIZ_Naming_CommonWords(_Index, _Word)
AND
ObjectGetFlag(_Player, _DisabledFlag, 1)
THEN
ObjectClearFlag(_Player, _DisabledFlag);

QRY
LLNWIZ_QRY_Naming_DisableWordIfNotAvailable((CHARACTERGUID)_Player, (INTEGER)_Index, (STRING)_DisabledFlag)
THEN
DB_NOOP(1);
//END_REGION

//REGION HELPERS
PROC
LLNWIZ_Naming_Words_SetLastWord((CHARACTERGUID)_Player, (STRING)_Word, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_LastWord(_Player, _LastWord)
THEN
NOT DB_LLNWIZ_Naming_LastWord(_Player, _LastWord);

PROC
LLNWIZ_Naming_Words_SetLastWord((CHARACTERGUID)_Player, (STRING)_Word, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_DialogVariables("LastWord", _VariableName)
THEN
DB_LLNWIZ_Naming_LastWord(_Player, _Word);
DialogSetVariableStringForInstance(_Instance, _VariableName, _Word);
ObjectSetFlag(_Player, "LLNWIZ_Naming_LastWordExists");

PROC
LLNWIZ_Naming_Words_LoadLastWord((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_LastWord(_Player, _Word)
AND
DB_LLNWIZ_Naming_DialogVariables("LastWord", _VariableName)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableName, _Word);
ObjectSetFlag(_Player, "LLNWIZ_Naming_LastWordExists");

PROC
LLNWIZ_Naming_Words_LoadLastWord((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_LastWord(_Player, _)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LastWordExists", 1)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_LastWordExists");
//END_REGION

//REGION FLAG_ACTIONS
IF
ObjectFlagSet("LLNWIZ_Naming_NextWordPage", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_NextWordPage")
THEN
LLNWIZ_Naming_Words_NextPage(_Player, _Instance);
LLNWIZ_Naming_Words_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_PreviousWordPage", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_PreviousWordPage")
THEN
LLNWIZ_Naming_Words_PreviousPage(_Player, _Instance);
LLNWIZ_Naming_Words_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_FirstWordPage", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_FirstWordPage")
THEN
LLNWIZ_Naming_Words_SetPage(_Player, 0, _Instance);
LLNWIZ_Naming_Words_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_LastWordPage", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_LastWordPage")
AND
DB_LLNWIZ_Naming_MaxWordPage(_LastIndex, _LastPageIndex, _MaxPage)
THEN
LLNWIZ_Naming_Words_SetPage(_Player, _LastPageIndex, _Instance);
LLNWIZ_Naming_Words_LoadPageVariables(_Player, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_LoadWords", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_LoadWords")
THEN
LLNWIZ_Naming_Words_ResetPlayerPage(_Player);
LLNWIZ_Naming_Words_LoadPageVariables(_Player, _Instance);
LLNWIZ_Naming_Words_LoadLastWord(_Player, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_SelectedLastWord", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_SelectedLastWord")
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 0)
AND
DB_LLNWIZ_Naming_LastWord(_Player, _LastWord)
THEN
LLNWIZ_Naming_AppendWord(_LastWord, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_RemoveLastWord", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_RemoveLastWord")
THEN
LLNWIZ_Naming_UndoLast(_Instance);

IF
ObjectFlagSet(_SelectFlag, (CHARACTERGUID)_Player, _Instance)
AND
DB_LLNWIZ_Naming_WordVariables(_WordIndex, _DialogVar, _SelectFlag, _DisabledFlag)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, _SelectFlag)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 0)
AND
DB_LLNWIZ_Temp_WordVariableValue(_WordIndex, _Word)
THEN
LLNWIZ_Naming_AppendWord(_Word, _Instance);
LLNWIZ_Naming_Words_SetLastWord(_Player, _Word, _Instance);
//DebugBreak("[LLNWIZ] User appended word.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
