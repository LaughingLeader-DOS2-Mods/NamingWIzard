Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
//DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle)
//DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard)
//DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text)
//DB_LLNWIZ_Wizard_LastGreeting(_Index)
//DB_LLNWIZ_Temp_NextWizardGreeting(_Text)

LLNWIZ_Wizard_InitializeSettings();
KBSECTION
//REGION SETTINGS
PROC
LLNWIZ_Wizard_AddGreeting((STRING)_Text)
AND
SysCount("DB_LLNWIZ_Wizard_RandomGreeting", 2, _Index)
THEN
DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text);

PROC
LLNWIZ_Wizard_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Temp_NextWizardGreeting", 1);
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Wizard_LastGreeting", 1);

LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Wizard_RandomGreeting", 2);
LLNWIZ_Wizard_AddGreeting("Welcome. What shall we name today?");
LLNWIZ_Wizard_AddGreeting("Beep, boop. I am the Naming Wizard. How may I serv-o you today?");
LLNWIZ_Wizard_AddGreeting("I once had I dream I existed within a moddable user interface. Can you imagine?");
LLNWIZ_Wizard_AddGreeting("I'm the Wiz, and nobody can beat my prices!");
LLNWIZ_Wizard_AddGreeting("Renameo Exacteo. Your wish is my command.");
LLNWIZ_Wizard_AddGreeting("Namelolocus Renamicus. Your wish is my command.");
LLNWIZ_Wizard_AddGreeting("Names are power. Let's get crackin'.");

PROC
LLNWIZ_System_ClearDatabases()
THEN
SysClear("DB_LLNWIZ_Temp_NextWizardGreeting", 1);

PROC
LLNWIZ_System_ClearDatabases()
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
ObjectExists(_Wizard, 1)
THEN
SetOnStage(_Wizard, 0);
CharacterDie(_Wizard, 0, "None");
NOT DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player);

//END_REGION

//REGION GREETING
QRY
LLNWIZ_QRY_Wizard_Internal_RollAgainIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
NOT DB_LLNWIZ_Wizard_LastGreeting(_Index)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_Internal_RollAgainIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
DB_LLNWIZ_Wizard_LastGreeting(_Index)
AND
LLNWIZ_Random(_Max)
THEN
DB_NOOP(1);


QRY
LLNWIZ_QRY_Wizard_Internal_RollIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
NOT DB_LLNWIZ_Wizard_LastGreeting(_Index)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_Internal_RollIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
DB_LLNWIZ_Wizard_LastGreeting(_Index)
AND
LLNWIZ_Random(_Max)
AND
DB_LLNWIZ_Temp_Rand(_NextIndex)
AND
LLNWIZ_QRY_Wizard_Internal_RollAgainIfLastGreeting(_NextIndex, _Max)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_RandomGreetingIndex()
AND
SysCount("DB_LLNWIZ_Wizard_RandomGreeting", 2, _TotalGreetings)
AND
IntegerSubtract(_TotalGreetings, 1, _MaxIndex)
AND
LLNWIZ_Random(_MaxIndex)
AND
DB_LLNWIZ_Temp_Rand(_Index)
AND
LLNWIZ_QRY_Wizard_Internal_RollIfLastGreeting(_Index, _MaxIndex)
THEN
DB_NOOP(1);

//0 is for no wizards, 1 is for a wizard.
PROC
LLNWIZ_Wizard_SetRandomGreeting(0)
THEN
DB_LLNWIZ_Temp_NextWizardGreeting("Naming Wizard Menu");

PROC
LLNWIZ_Wizard_SetRandomGreeting(1)
AND
SysCount("DB_LLNWIZ_Wizard_RandomGreeting", 2, _TotalGreetings)
AND
IntegerSubtract(_TotalGreetings, 1, _MaxIndex)
AND
LLNWIZ_QRY_Wizard_RandomGreetingIndex()
AND
DB_LLNWIZ_Temp_Rand(_Index)
AND
DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text)
THEN
DB_LLNWIZ_Temp_NextWizardGreeting(_Text);
LLNWIZ_Wizard_SetLastGreeting(_Index);

PROC
LLNWIZ_Wizard_SetLastGreeting((INTEGER)_Index)
AND
DB_LLNWIZ_Wizard_LastGreeting(_LastIndex)
THEN
NOT DB_LLNWIZ_Wizard_LastGreeting(_LastIndex);

PROC
LLNWIZ_Wizard_SetLastGreeting((INTEGER)_Index)
THEN
DB_LLNWIZ_Wizard_LastGreeting(_Index);

//END_REGION

//Creation
QRY
LLNWIZ_QRY_Wizard_CreateWizard((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
NOT DB_LLNWIZ_Wizard_CreatedWizard(_, _Player)
AND
GetPosition(_Target, _x,_y,_z)
AND
//TemporaryCharacterCreateAtPosition(_x,_y,_z, "LLNWIZ_NamingWizard_923ec2b9-f8c0-4fd9-8647-5459f6780b5a", 0, _Wizard)
TemporaryCharacterCreateOutOfSightToObject("LLNWIZ_NamingWizard_923ec2b9-f8c0-4fd9-8647-5459f6780b5a", _Target, _Player, 0, "", _Wizard)
THEN
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player);
TeleportToPosition(_Wizard, _x, _y, _z, "LLNWIZ_Events_WizardCreated", 0, 0);

QRY
LLNWIZ_QRY_Wizard_CreateWizard((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
GetPosition(_Target, _x,_y,_z)
THEN
TeleportToPosition(_Wizard, _x, _y, _z, "LLNWIZ_Events_WizardCreated", 0, 0);
SetOnStage(_Wizard, 1);

IF
StoryEvent((CHARACTERGUID)_Wizard, "LLNWIZ_Events_WizardCreated")
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
PlayLoopEffect(_Wizard, "LLNWIZ_FX_NamingWIzard_Book_01", "Hand_BodyFX", _Handle)
THEN
DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle);
CharacterLookAt(_Wizard, _Player);
LLNWIZ_PlayEffectAtTargetPosition(_Wizard, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
LLNWIZ_Naming_OpenNamingWizard(_Player, _Wizard);

//Menu Closed
IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLNWIZ_MenuDialog(_Dialog)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle)
AND
GetUUID(_Wizard, _ID)
AND
StringConcatenate("LLNWIZ_Timers_Wizard_DisappearDelay_", _ID, _TimerName)
THEN
StopLoopEffect(_Handle);
DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard);
TimerLaunch(_TimerName, 1100);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard)
THEN
LLNWIZ_PlayEffectAtTargetPosition(_Wizard, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
SetOnStage(_Wizard, 0);
NOT DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
