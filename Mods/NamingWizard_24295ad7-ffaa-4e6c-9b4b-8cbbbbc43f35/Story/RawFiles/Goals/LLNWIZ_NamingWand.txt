Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLNWIZ_NamingWand_InitializeSettings();

//DB_LLNWIZ_NamingWand_Skills(_Skill)
//DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
//DB_LLNWIZ_NamingWand_TimerDelay(_SkillUseDelay, _CastDelay)
//DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
KBSECTION
PROC
LLNWIZ_NamingWand_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_NamingWand_Skills", 1);
DB_LLNWIZ_NamingWand_Skills("Target_LLNWIZ_Rename");

LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_NamingWand_TimerDelay", 2);
DB_LLNWIZ_NamingWand_TimerDelay(3000, 700);

PROC
LLNWIZ_NamingWand_ClearPreviousTargets((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
THEN
NOT DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill);

PROC
LLNWIZ_NamingWand_CreateFailTimer((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
GetUUID(_Player, _ID)
AND
StringConcatenate("LLNWIZ_Timers_RenamingFailedTimer_", _ID, _TimerName)
THEN
TimerLaunch(_TimerName, 4000);
DB_LLNWIZ_NamingWand_FailTimer(_TimerName, _Player, _Target);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_NamingWand_FailTimer(_TimerName, _Player, _Target)
THEN
CharacterStatusText(_Player, "<font color='#FF0000'>Renaming Failed!</font>");
LLNWIZ_Wizard_HideWizardByTarget(_Target);
NOT DB_LLNWIZ_NamingWand_FailTimer(_TimerName, _Player, _Target);

IF
CharacterUsedSkillOnTarget(_Player, (CHARACTERGUID)_Target, _Skill, _)
AND
DB_LLNWIZ_NamingWand_Skills(_Skill)
AND
GetUUID(_Player, _ID)
AND
StringConcatenate("LLNWIZ_Timers_RenameTarget_", _ID, _TimerName)
AND
DB_LLNWIZ_NamingWand_TimerDelay(_SkillUseDelay, _CastDelay)
THEN
LLNWIZ_NamingWand_ClearPreviousTargets(_Player);
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill);
TimerLaunch(_TimerName, _SkillUseDelay); // Failsafe, in case the skill never reaches a "cast"

IF
SkillCast(_Player, _Skill, _)
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
AND
DB_LLNWIZ_NamingWand_TimerDelay(_SkillUseDelay, _CastDelay)
THEN
ApplyStatus(_Target, "LLNWIZ_RENAMING", -1.0, 1, _Player);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, _CastDelay);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
AND
CharacterGetDisplayName(_Target, _Handle, _Name)
AND
LLNWIZ_QRY_Wizard_CreateWizard(_Player, _Target)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
THEN
LLNWIZ_Naming_SetTargetName(_Name, _Player);

IF
StoryEvent((CHARACTERGUID)_Player, "LLNWIZ_Events_NamingComplete")
AND
DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill)
AND
ObjectExists(_Target, 1)
AND
GetVarString(_Player, "LLNWIZ_FinalText", _Name)
THEN
CharacterSetCustomName(_Target, _Name);
ClearVarObject(_Player, "LLNWIZ_FinalText");
NOT DB_LLNWIZ_NamingWand_RenameTarget(_TimerName, _Player, _Target, _Skill);
ObjectClearFlag(_Player, "LLNWIZ_Naming_TextSaved");

IF
StoryEvent((CHARACTERGUID)_Player, "LLNWIZ_Events_NamingCancelled")
THEN
LLNWIZ_NamingWand_ClearPreviousTargets(_Player);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
