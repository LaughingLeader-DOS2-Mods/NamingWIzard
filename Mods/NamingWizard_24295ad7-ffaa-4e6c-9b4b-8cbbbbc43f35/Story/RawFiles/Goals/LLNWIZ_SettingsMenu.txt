Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLNWIZ_SettingsMenu_InitializeSettings();

//DB_Temp_InSettingsMenu(_Player);
//DB_Temp_SettingsMenuBookTimerActive(_Active)
KBSECTION
//REGION SETTINGS
PROC
LLNWIZ_SettingsMenu_InitializeSettings()
THEN
DB_NOOP(1);

PROC
LeaderUpdater_ModUpdated("NamingWizard", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LeaderLib_StringExt_QRY_VersionIsLessThan(_PastVersion, 1, 0, 2, 0)
THEN
SysClear("DB_SettingsMenu_FlagSetMessage", 2);
SysClear("DB_SettingsMenu_FlagClearedMessage", 2);
//END_REGION

//REGION GLOBAL_FLAG_MESSAGES

IF
ObjectFlagSet("LLNWIZ_DisplayHostPermissionMessage", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "LLNWIZ_DisplayHostPermissionMessage");
CharacterStatusText(_Player, "LLNWIZ_StatusText_Settings_HostPermissionWarning");

IF
GlobalFlagSet("LLNWIZ_Settings_RenameWandDisabled")
AND
DB_Temp_InSettingsMenu(_Player)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_Settings_RenameWandDisabled");

IF
GlobalFlagCleared("LLNWIZ_Settings_RenameWandDisabled")
AND
DB_Temp_InSettingsMenu(_Player)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_Settings_RenameWandEnabled");

IF
GlobalFlagSet("LLNWIZ_Settings_CrossPlayerRenamingEnabled")
AND
DB_Temp_InSettingsMenu(_Player)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_Settings_CrossPlayerRenamingEnabled");

IF
GlobalFlagCleared("LLNWIZ_Settings_CrossPlayerRenamingEnabled")
AND
DB_Temp_InSettingsMenu(_Player)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_Settings_CrossPlayerRenamingDisabled");
//END_REGION

//REGION COMMAND_FLAGS
IF
ObjectFlagSet("LLNWIZ_Commands_AddRenamingWand", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Commands_AddRenamingWand")
AND
DB_LLNWIZ_ItemTemplates("RenameWand", _Template)
THEN
ItemTemplateAddTo(_Template, _Player, 1, 1);
CharacterStatusText(_Player, "LLNWIZ_StatusText_WandAdded");
//END_REGION

//REGION BOOK_SPAWNING
//Basically deprecated due to the Mod Settings Menu
/*
IF
GameStarted(_Level,_)
AND
IsGameLevel(_Level, 1)
AND
NOT DB_Temp_SettingsMenuBookTimerActive(_)
THEN
DB_Temp_SettingsMenuBookTimerActive(1);
TimerLaunch("LLNWIZ_Timers_CheckForSettingsBook", 2000);

IF
TimerFinished("LLNWIZ_Timers_CheckForSettingsBook")
AND
DB_Temp_SettingsMenuBookTimerActive(1)
AND
NOT GlobalGetFlag("LeaderLib_Active", 1)
AND
DB_LLNWIZ_ItemTemplates("SettingsBook", _Template)
AND
CharacterGetHostCharacter(_Host)
AND
NOT GetItemForItemTemplateInPartyInventory(_Host, _Template, _)
THEN
ItemTemplateAddTo(_Template, _Host, 1, 1);

IF
TimerFinished("LLNWIZ_Timers_CheckForSettingsBook")
THEN
NOT DB_Temp_SettingsMenuBookTimerActive(1);
*/
//END_REGION

//REGION DIALOG
IF
CharacterUsedItemTemplate(_Player, "BOOK_LLNWIZ_Settings_a66e3eae-8ea3-43b1-8db7-0e736ad53336", _Book)
THEN
LLNWIZ_Settings_OpenMenu(_Player);

IF
DialogStarted("LLNWIZ_NamingWizardSettings", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
NOT DB_Temp_InSettingsMenu(_Player)
THEN
DB_Temp_InSettingsMenu(_Player);

IF
DialogEnded("LLNWIZ_NamingWizardSettings", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_Temp_InSettingsMenu(_Player)
THEN
NOT DB_Temp_InSettingsMenu(_Player);

PROC
LLNWIZ_Settings_OpenMenu((CHARACTERGUID)_Player)
AND
CharacterIsInCombat(_Player, 0)
AND
DB_LLNWIZ_Dialog("Settings", _DialogName)
THEN
Proc_StartDialog(0, _DialogName, _Player, _Player);

PROC
LLNWIZ_Settings_OpenMenu((CHARACTERGUID)_Player)
AND
CharacterIsInCombat(_Player, 1)
THEN
CharacterStatusText(_Player, "LLNWIZ_StatusText_CombatWarning");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
