Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLNWIZ_Dialog("NamingWizard", _DialogName)
//DB_LLNWIZ_ItemTemplates(_Name, _Template)
//DB_LLNWIZ_System_PlayerInitialized(_Val)

//DB_LLNWIZ_Temp_OutVariable(_ID, _Value)
//DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _Value)
//DB_LLNWIZ_Random_Seed(_Seed)
//DB_LLNWIZ_Temp_Rand(_OldRand)

LLNWIZ_System_InitializeSettings();
KBSECTION
//REGION DATABASE_HELPERS
QRY
LLNWIZ_QRY_System_DatabaseExists((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Count)
AND
_Count > 0
THEN
DB_NOOP(1);

PROC
LLNWIZ_System_ClearExistingDatabase((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Count)
AND
_Count > 0
THEN
SysClear(_DatabaseName, _Arity);

PROC
LLNWIZ_System_Debug_DatabaseCount((STRING)_DatabaseName, (INTEGER)_Arity)
AND
SysCount(_DatabaseName, _Arity, _Total)
AND
IntegertoString(_Total, _TotalStr)
AND
StringConcatenate("[Init] '", _DatabaseName, _Str1)
AND
StringConcatenate(_Str1, "' Total Entries: ", _Str2)
AND
StringConcatenate(_Str2, _TotalStr, _Str3)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ] ", _Str3);
//END_REGION

//REGION SETTINGS
PROC
LLNWIZ_System_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Dialog", 1);
DB_LLNWIZ_Dialog("NamingWizard", "LLNWIZ_NamingWizard");
DB_LLNWIZ_Dialog("Settings", "LLNWIZ_NamingWizardSettings");

LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_ItemTemplates", 2);
DB_LLNWIZ_ItemTemplates("RenameWand", "WPN_LLNWIZ_Wand_Renamer_0a65dc24-7785-48af-8266-9b5b55f6c683");
DB_LLNWIZ_ItemTemplates("SettingsBook", "BOOK_LLNWIZ_Settings_a66e3eae-8ea3-43b1-8db7-0e736ad53336");

SysClear("DB_LLNWIZ_Temp_OutVariable", 2);
SysClear("DB_LLNWIZ_Temp_TracedCharacterValue", 2);
//END_REGION

//REGION UPDATER
PROC
LLNWIZ_System_UpdateDatabases()
THEN
LLNWIZ_System_InitializeSettings();

PROC
LLNWIZ_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_Dialog", 1);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_ItemTemplates", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_System_PlayerInitialized", 1);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_Temp_OutVariable", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_Temp_TracedCharacterValue", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_Random_Seed", 1);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "System", "DB_LLNWIZ_Temp_Rand", 1);
LeaderLog_Log("TRACE", "[LLNWIZ:System] Databases registered.");
//END_REGION

//REGION QUERIES
QRY
LLNWIZ_QRY_UserMatch((CHARACTERGUID)_Char1, (CHARACTERGUID)_Char2)
AND
CharacterGetReservedUserID(_Char1, _ID)
AND
CharacterGetReservedUserID(_Char2, _ID)
THEN
DB_NOOP(1);
//END_REGION

//REGION CHARACTER_TRACING
QRY
LLNWIZ_System_GetTracedCharacterValue((CHARACTERGUID)_Character, (STRING)_ValueName)
AND
DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _Value)
THEN
NOT DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _Value);

QRY
LLNWIZ_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "nameref")
AND
CharacterGetDisplayName(_Character, _Handle, _Ref)
THEN
DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _Ref);

QRY
LLNWIZ_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "namehandle")
AND
CharacterGetDisplayName(_Character, _Handle, _Ref)
THEN
DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _Handle);

QRY
LLNWIZ_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "id")
AND
GetUUID(_Character, _ID)
THEN
DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _ID);

QRY
LLNWIZ_System_GetTracedCharacterValue((CHARACTERGUID)_Character, "template")
AND
GetTemplate(_Character, _Template)
THEN
DB_LLNWIZ_Temp_TracedCharacterValue(_Character, _Template);
//END_REGION

//REGION TEMP_VARIABLES
PROC
LLNWIZ_SetTemporaryVariable((STRING)_ID, (STRING)_Value)
AND
DB_LLNWIZ_Temp_OutVariable(_ID, _CurrentValue)
THEN
NOT DB_LLNWIZ_Temp_OutVariable(_ID, _CurrentValue);

PROC
LLNWIZ_SetTemporaryVariable((STRING)_ID, (STRING)_Value)
THEN
DB_LLNWIZ_Temp_OutVariable(_ID, _Value);

QRY
LLNWIZ_QRY_SetTemporaryVariable((STRING)_ID, (STRING)_Value)
THEN
LLNWIZ_SetTemporaryVariable(_ID, _Value);
//END_REGION

//REGION FLAG_HELPERS
QRY
LLNWIZ_QRY_ClearObjectFlag((GUIDSTRING)_Object, (STRING)_Flag)
THEN
ObjectClearFlag(_Object, _Flag);
//END_REGION

//REGION FX_HELPERS
PROC
LLNWIZ_PlayEffectAtTargetPosition((CHARACTERGUID)_Target, (STRING)_Effect)
AND
GetPosition(_Target,_x,_y,_z)
THEN
PlayEffectAtPosition(_Effect, _x, _y, _z);
//END_REGION

//REGION EVENT_HELPERS
PROC
LLNWIZ_SendStoryEvent((STRING)_Event)
AND
DB_IsPlayer(_Player)
AND
NOT DB_LLNWIZ_Temp_FiringEvent(_Event)
THEN
DB_LLNWIZ_Temp_FiringEvent(_Event);
SetStoryEvent(_Player, _Event);
LeaderLog_Log("DEBUG", "[LLNWIZ:System] Firing general story event [",_Event,"]");

PROC
LLNWIZ_SendStoryEvent((STRING)_Event)
AND
NOT DB_IsPlayer(_)
AND
NOT DB_LLNWIZ_Temp_FiringEventWorkaround(_Event)
THEN
DB_LLNWIZ_Temp_FiringEventWorkaround(_Event);
LeaderLog_Log("DEBUG", "[LLNWIZ:System][ERROR] Error firing story event [",_Event,"]: Can't find a player! Using [CharacterLaunchIterator] as a workaround.");
CharacterLaunchIterator("LLNWIZ_Events_System_FindCharacter");

IF
StoryEvent(_Character,"LLNWIZ_Events_System_FindCharacter")
AND
DB_LLNWIZ_Temp_FiringEventWorkaround(_Event)
THEN
DB_LLNWIZ_Temp_FiringEvent(_Event);
SetStoryEvent(_Character, _Event);
NOT DB_LLNWIZ_Temp_FiringEventWorkaround(_Event);

IF
StoryEvent(_,_Event)
AND
DB_LLNWIZ_Temp_FiringEvent(_Event)
THEN
NOT DB_LLNWIZ_Temp_FiringEvent(_Event);
LeaderLog_Log("DEBUG", "[LLNWIZ:System] Finished firing story event [",_Event,"]");
//END_REGION

//REGION RANDOM
//Thanks to Norbyte for this chunk
QRY
LLNWIZ_Random((INTEGER)_Modulo)
AND
NOT DB_LLNWIZ_Random_Seed(_)
AND
Random(99999, _Seed)
THEN
DB_LLNWIZ_Random_Seed(_Seed);

QRY
LLNWIZ_Random((INTEGER)_Modulo)
AND
NOT DB_LLNWIZ_Temp_Rand(_)
AND
Random(99999, _OldRand)
THEN
DB_LLNWIZ_Temp_Rand(_OldRand);

QRY
LLNWIZ_Random((INTEGER)_Modulo)
AND
DB_LLNWIZ_Random_Seed(_Seed)
AND
IntegerProduct(_Seed, 1664525, _SeedLCG1)
AND
IntegerSum(_SeedLCG1, 1013904223, _SeedLCG2)
AND
IntegerModulo(_SeedLCG2, _Modulo, _Rand)
AND
DB_LLNWIZ_Temp_Rand(_OldRand)
THEN
NOT DB_LLNWIZ_Random_Seed(_Seed);
DB_LLNWIZ_Random_Seed(_SeedLCG2);
NOT DB_LLNWIZ_Temp_Rand(_OldRand);
DB_LLNWIZ_Temp_Rand(_Rand);
//END_REGION

//REGION DEBUG_LEVEL
IF
GameStarted("LLNWIZ_DebugLevel", _)
AND
NOT SysIsActive("LLNWIZ_Debug")
THEN
SysActivateGoal("LLNWIZ_Debug");
LeaderLog_Log("DEBUG", "[LLNWIZ:System] Debug level starting. Activating LLNWIZ_Debug.");

IF
GameStarted(_Level, 0)
AND
_Level != "LLNWIZ_Debug"
AND
SysIsActive("LLNWIZ_Debug")
THEN
SysCompleteGoal("LLNWIZ_Debug");
LeaderLog_Log("DEBUG", "[LLNWIZ:System] Debug level ending. Deactivating LLNWIZ_Debug.");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
