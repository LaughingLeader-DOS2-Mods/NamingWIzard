Version 1
SubGoalCombiner SGC_AND
INITSECTION
LeaderUpdater_RegisterMod("NamingWizard", "0.9.0.0", "LaughingLeader");
LeaderUpdater_RegisterActiveGoal("NamingWizard", "LaughingLeader", "LLNWIZ_ZZZ_Updater");
KBSECTION
//REGION MOD_UPDATING
PROC
LLNWIZ_System_UpdateDatabases()
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater] Databases updated.");

PROC
LLNWIZ_System_ClearDatabases()
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater] Databases cleared.");

PROC
LLNWIZ_Updater_ResetDatabases()
THEN
LLNWIZ_System_ClearDatabases();
//LLNWIZ_Updater_RegisterMod("0.9.0.0");
LLNWIZ_System_UpdateDatabases();
LeaderLog_Log("DEBUG", "[LLNWIZ] ", "[Updater] Databases cleared and re-initialized.");
//END_REGION

//REGION UPDATE_EVENT
IF
GameStarted(_,_)
AND
LeaderUpdater_QRY_ModUpdateNeeded("NamingWizard", "0.9.0.0", "LaughingLeader")
THEN
LeaderUpdater_RegisterMod("NamingWizard", "0.9.0.0", "LaughingLeader");
LLNWIZ_System_UpdateDatabases();
//END_REGION

//REGION UNINSTALLING
PROC
LLNWIZ_Updater_UnregisterMod()
THEN
LeaderUpdater_UnregisterMod("NamingWizard", "LaughingLeader");

PROC
LLNWIZ_Updater_UnregisterMod()
THEN
GlobalClearFlag("LLNWIZ_NAMING_WIZARD_ACTIVE");

PROC
LLNWIZ_Updater_UninstallMod()
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Uninstalling Naming Wizard.");
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Attempting to end all Naming Wizard dialogs. Resuming in 2 seconds.");
LLNWIZ_Updater_CloseAllNamingWizardDialogs();
TimerLaunch("LLNWIZ_Timers_Updater_UninstallerPhase2", 2000);

IF
TimerFinished("LLNWIZ_Timers_Updater_UninstallerPhase2")
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Clearing all flags.");
LLNWIZ_Updater_ClearFlags();
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Destroying all Naming Wizard items.");
LLNWIZ_Updater_DestroyNamingWizardItems();
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Clearing databases.");
LLNWIZ_System_ClearDatabases();
LeaderLog_Log("DEBUG", "[LLNWIZ][Updater:Uninstaller] Unregistering Naming Wizard.");
LLNWIZ_Updater_UnregisterMod();

PROC
LLNWIZ_Updater_CloseAllNamingWizardDialogs()
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
DialogRequestStop(_Player);

PROC
LLNWIZ_Updater_ClearFlags()
THEN
LLNWIZ_Settings_ClearGlobalFlags();

PROC
LLNWIZ_Updater_ClearFlags()
AND
DB_IsPlayer(_Player)
THEN
LLNWIZ_Settings_ClearObjectFlags(_Player);
LLNWIZ_Settings_ClearUserFlags(_Player);

PROC
LLNWIZ_Updater_UnequipNamingWizardItem((CHARACTERGUID)_Player, (ITEMGUID)_Item)
AND
ItemIsEquipable(_Item, 1)
THEN
CharacterUnequipItem(_Player, _Item);

PROC
LLNWIZ_Updater_DestroyNamingWizardItems()
AND
DB_IsPlayer(_Player)
AND
DB_LLNWIZ_ItemTemplates(_Name, _Template)
AND
GetItemForItemTemplateInInventory(_Player, _Template, _Item)
THEN
LLNWIZ_Updater_UnequipNamingWizardItem(_Player, _Item);
ItemDestroy(_Item);
//END_REGION

//REGION ACTIVE_FLAG
IF
GameStarted(_,_)
AND
GlobalGetFlag("LLNWIZ_NAMING_WIZARD_ACTIVE", 0)
THEN
GlobalSetFlag("LLNWIZ_NAMING_WIZARD_ACTIVE");
//END_REGION
EXITSECTION
LLNWIZ_Updater_UnregisterMod();
ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
