Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLNWIZ_Naming_InitializeSettings();
LLNWIZ_Naming_SetMaxCharacterLength(70);

//DB_LLNWIZ_Dialog("NamingWizard", _DialogName)
//DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text)
//DB_LLNWIZ_Wizard_LastGreeting(_Index)
//DB_LLNWIZ_Temp_NextWizardGreeting(_Text)

//DB_LLNWIZ_Naming_DialogVariables(_Shortname, _VariableName)

//DB_LLNWIZ_Naming_StartText(_Text, _Player);
//DB_LLNWIZ_Naming_VisibleText(_Text, _Instance);
//DB_LLNWIZ_Naming_FinishedText(_Text, _Player, _Length);
//DB_LLNWIZ_Naming_LastFinishedText(_Text, _Player, _Length);
//DB_LLNWIZ_Naming_Codex(_Group, _Index, _Letter)
//DB_LLNWIZ_Naming_TargetName(_Player, _Name)
//DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName)

//DB_LLNWIZ_Naming_Groups(_Index, _Group)
//DB_LLNWIZ_Naming_CurrentGroup(_Index, _Instance)
//DB_LLNWIZ_Naming_GroupMax(_Group, _Max)
//DB_LLNWIZ_Naming_CurrentCasing(_Group, _Instance)

//DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
//DB_LLNWIZ_Naming_TextLength(_Length, _Instance);
//DB_LLNWIZ_Naming_MaxLength(_MaxLength)

//DB_LLNWIZ_Naming_Temp_TextLength(_Text, _Instance, _Length)
//DB_LLNWIZ_Naming_Temp_FoundWord(_Text, _StartIndex, _StartLength, _StartIndex2, _StartLength2)
//DB_LLNWIZ_Naming_Temp_LastVisibleText(_LastText, _Instance)
KBSECTION
//REGION SETTINGS_COUNTING
PROC
LLNWIZ_Naming_ConfigureCountingDatabase()
AND
DB_LLNWIZ_Naming_Init_CharStart(_Index)
AND
IntegerSum(_Index, 1, _NextIndex)
AND
_NextIndex <= 255
THEN
DB_LLNWIZ_Naming_CharIndex(_NextIndex);
NOT DB_LLNWIZ_Naming_Init_CharStart(_Index);
DB_LLNWIZ_Naming_Init_CharStart(_NextIndex);

PROC
LLNWIZ_Naming_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_CharIndex", 1);
DB_LLNWIZ_Naming_CharIndex(0);
DB_LLNWIZ_Naming_Init_CharStart(0);
TimerLaunch("LLNWIZ_Timers_CharIndexInit", 100);

IF
TimerFinished("LLNWIZ_Timers_CharIndexInit")
AND
DB_LLNWIZ_Naming_Init_CharStart(_Index)
AND
_Index < 256
THEN
LLNWIZ_Naming_ConfigureCountingDatabase();
TimerLaunch("LLNWIZ_Timers_CharIndexInit", 100);

IF
TimerFinished("LLNWIZ_Timers_CharIndexInit")
AND
DB_LLNWIZ_Naming_Init_CharStart(_Index)
AND
_Index >= 256
THEN
NOT DB_LLNWIZ_Naming_Init_CharStart(_Index);
//END_REGION

//REGION SETTINGS_DIALOG_VARS
PROC
LLNWIZ_Naming_InitialzeDialogVariables()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_DialogVariables", 2);
DB_LLNWIZ_Naming_DialogVariables("RandomGreeting", "LLNWIZ_NamingWizard_RandomGreeting_7b3afe6c-373f-4586-8b62-06cd89b2efc4");
DB_LLNWIZ_Naming_DialogVariables("SpeakerIndex", "LLNWIZ_Menu_SpeakerIndex_a58be75c-66f9-4c4d-9edb-8c6422f47c7c");
DB_LLNWIZ_Naming_DialogVariables("CurrentPage", "LLNWIZ_Naming_CurrentPage_90ca7bcc-e4c5-47d7-b055-0ac318380edf");
DB_LLNWIZ_Naming_DialogVariables("LastPage", "LLNWIZ_Naming_LastPage_7a9befad-b178-478e-b374-f65e7efcf94f");
DB_LLNWIZ_Naming_DialogVariables("SelectedLetter", "LLNWIZ_Naming_SelectedLetter_5a8bb119-885b-4035-8690-e225a095687c");
DB_LLNWIZ_Naming_DialogVariables("Text", "LLNWIZ_Naming_Text_06c2ac76-9529-4cf4-979f-d37911d55ef4");
DB_LLNWIZ_Naming_DialogVariables("FinalText", "LLNWIZ_Naming_FinalText_4bfd6bc7-3afd-474a-92af-99b170d226cd");
DB_LLNWIZ_Naming_DialogVariables("LastWord", "LLNWIZ_Naming_LastWord_fcc4d39a-1009-4037-9848-3838066ee9b7");
DB_LLNWIZ_Naming_DialogVariables("WordStateText", "LLNWIZ_Naming_WordStateText_9d81dba3-e7bd-4209-a5b1-793491e80fc4");
DB_LLNWIZ_Naming_DialogVariables("TargetName", "LLNWIZ_Naming_TargetName_36de6db5-815a-42c3-a8c9-6fe7c0ff1b35");
DB_LLNWIZ_Naming_DialogVariables("OriginalName", "LLNWIZ_Naming_OriginalCharacterName_c4ae7eac-1cd4-48ad-a48d-ab2a1a299319");
DB_LLNWIZ_Naming_DialogVariables("LastConfirmedText", "LLNWIZ_Naming_LastConfirmedText_27064f2f-b14b-4038-b853-edaf2cc1b480");
//END_REGION

//REGION SETTINGS_MAIN
PROC
LLNWIZ_Naming_SetMaxCharacterLength((INTEGER)_Max)
AND
_Max < 256
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_MaxLength", 1);
DB_LLNWIZ_Naming_MaxLength(_Max);

PROC
LLNWIZ_Naming_InitializeSettings()
THEN
//LLNWIZ_Naming_SetMaxCharacterLength(70); // May be overriden by a mod
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Naming_StartText", 2);
LLNWIZ_Naming_InitialzeDialogVariables();

//Stop on-going Naming Wizard dialogs if updating
PROC
LLNWIZ_System_UpdateDatabases()
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
DialogRequestStop(_Player);

PROC
LLNWIZ_System_UpdateDatabases()
THEN
LLNWIZ_Naming_InitializeSettings();

PROC
LLNWIZ_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_StartText", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_VisibleText", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_FinishedText", 3);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_LastFinishedText", 3);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_CurrentGroup", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_CurrentCasing", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_SelectedLetter", 3);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_TextLength", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_MaxLength", 1);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_DialogVariables", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_TargetName", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_TargetCustomName", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_Temp_FoundWord", 5);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_Naming_Temp_LastVisibleText", 2);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard", "DB_LLNWIZ_NamingWand_TimerDelay", 2);
LeaderLog_Log("TRACE", "[LLNWIZ:Settings:NamingWizard] Databases registered.");
//END_REGION

//REGION CLEAR
PROC
LLNWIZ_Naming_ClearSelectedLetter((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
THEN
NOT DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance);

PROC
LLNWIZ_Naming_ClearCurrentGroup((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentGroup(_Index, _Instance)
THEN
NOT DB_LLNWIZ_Naming_CurrentGroup(_Index, _Instance);

PROC
LLNWIZ_Naming_ClearInstanceVariables((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentCasing(_ActualGroup, _Instance)
THEN
NOT DB_LLNWIZ_Naming_CurrentCasing(_ActualGroup, _Instance);

PROC
LLNWIZ_Naming_ClearInstanceVariables((INTEGER)_Instance)
THEN
LLNWIZ_Naming_ClearSelectedLetter(_Instance);

PROC
LLNWIZ_Naming_ClearInstanceVariables((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
THEN
NOT DB_LLNWIZ_Naming_VisibleText(_Text, _Instance);

PROC
LLNWIZ_Naming_ClearPlayerVariables((CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_CanUndo", 1)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_CanUndo");

PROC
LLNWIZ_Naming_ClearPlayerVariables((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_TargetName(_Player, _Name)
THEN
NOT DB_LLNWIZ_Naming_TargetName(_Player, _Name);
ObjectClearFlag(_Player, "LLNWIZ_Naming_TargetNameSet");

PROC
LLNWIZ_Naming_ClearPlayerVariables((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName)
THEN
NOT DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName);

PROC
LLNWIZ_Naming_ClearPlayerVariables((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_FinishedText(_Text, _Player, _Length)
THEN
NOT DB_LLNWIZ_Naming_FinishedText(_Text, _Player, _Length);

PROC
LLNWIZ_Naming_ClearPlayerVariables((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_StartText(_Text, _Player)
THEN
NOT DB_LLNWIZ_Naming_StartText(_Text, _Player);

PROC
LLNWIZ_Naming_ClearSessionVariables((CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_CancelledSaving");
ObjectClearFlag(_Player, "LLNWIZ_Naming_TextSaved");
SetVarString(_Player, "LLNWIZ_FinalText", "");
LLNWIZ_Naming_ClearPlayerVariables(_Player);
LeaderLog_Log("DEBUG", "[LLNWIZ:NamingWizard] Cleared saved session variables for player.");
//END_REGION

//REGION QUERIES
QRY
LLNWIZ_Naming_QRY_TargetNameVariableSet((CHARACTERGUID)_Player)
AND
GetVarString(_Player, "LLNWIZ_TargetName", _Name)
AND
_Name != ""
THEN
DB_NOOP(1);

QRY
LLNWIZ_Naming_QRY_CustomNameVariableSet((CHARACTERGUID)_Player)
AND
GetVarString(_Player, "LLNWIZ_CustomName", _Name)
AND
_Name != ""
THEN
DB_NOOP(1);
//END_REGION

//REGION TEXT_SETTINGS
PROC
LLNWIZ_Naming_SetLastText((STRING)_Text, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_Temp_LastVisibleText(_LastText, _Instance)
THEN
NOT DB_LLNWIZ_Naming_Temp_LastVisibleText(_LastText, _Instance);

PROC
LLNWIZ_Naming_SetLastText((STRING)_Text, (INTEGER)_Instance)
THEN
DB_LLNWIZ_Naming_Temp_LastVisibleText(_Text, _Instance);

PROC
LLNWIZ_Naming_SetText((STRING)_Text, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_VisibleText(_, _Instance)
THEN
LLNWIZ_Naming_SetLastText("", _Instance);

PROC
LLNWIZ_Naming_SetText((STRING)_Text, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_CurrentText, _Instance)
THEN
NOT DB_LLNWIZ_Naming_VisibleText(_CurrentText, _Instance);
LLNWIZ_Naming_SetLastText(_CurrentText, _Instance);

PROC
LLNWIZ_Naming_SetText((STRING)_Text, (INTEGER)_Instance)
THEN
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance);

PROC
LLNWIZ_Naming_SetText((STRING)_Text, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_CurrentText, _Instance)
AND
DB_LLNWIZ_Naming_DialogVariables("Text", _VariableName)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableName, _CurrentText);

PROC
LLNWIZ_Naming_SetLength((INTEGER)_Length, (INTEGER)_Instance)
AND 
DB_LLNWIZ_Naming_TextLength(_CurrentLength, _Instance)
THEN
NOT DB_LLNWIZ_Naming_TextLength(_CurrentLength, _Instance);

PROC
LLNWIZ_Naming_SetLength((INTEGER)_Length, (INTEGER)_Instance)
THEN
DB_LLNWIZ_Naming_TextLength(_Length, _Instance);
LLNWIZ_Naming_CheckMaxLength(_Instance);

PROC
LLNWIZ_Naming_IncrementLength((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
AND
IntegerSum(_Length, 1, _NewLength)
THEN
LLNWIZ_Naming_SetLength(_NewLength, _Instance);

PROC
LLNWIZ_Naming_IncrementLength((INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_TextLength(_, _Instance)
THEN
LLNWIZ_Naming_SetLength(0, _Instance);

QRY
LLNWIZ_QRY_Naming_DecreaseLength((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
AND
_Length > 0
AND
IntegerSubtract(_Length, 1, _NewLength)
THEN
LLNWIZ_Naming_SetLength(_NewLength, _Instance);

QRY
LLNWIZ_QRY_Naming_DecreaseLength((INTEGER)_Instance)
THEN
DB_NOOP(1);
//END_REGION

//REGION LETTER_CHANGING
PROC
LLNWIZ_Naming_AppendLetter((STRING)_Letter, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
StringConcatenate(_Text, _Letter, _NextText)
THEN
LLNWIZ_Naming_SetText(_NextText, _Instance);
LLNWIZ_Naming_IncrementLength(_Instance);

PROC
LLNWIZ_Naming_AppendLetter((STRING)_Letter, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_VisibleText(_,_Instance)
THEN
LLNWIZ_Naming_SetText(_Letter, _Instance);
LLNWIZ_Naming_SetLength(1, _Instance);

PROC
LLNWIZ_Naming_RemoveLastLetter((INTEGER)_Instance)
AND
LLNWIZ_QRY_Naming_DecreaseLength(_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
AND
StringSub(_Text, 0, _Length, _NewText)
THEN
LLNWIZ_Naming_SetText(_NewText, _Instance);

//To do
QRY
LLNWIZ_QRY_Naming_FindWord((STRING)_Text, (STRING)_Word)
THEN
DB_LLNWIZ_Naming_Temp_FoundWord(_Text, 0, 0, 0, 0);

PROC
LLNWIZ_Naming_RemoveWord((INTEGER)_Instance, (STRING)_Word)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
StringContains(_Text, _Word, 1)
AND
LLNWIZ_QRY_Naming_FindWord(_Text, _Word)
AND
DB_LLNWIZ_Naming_Temp_FoundWord(_Text, _StartIndex, _StartLength, _StartIndex2, _StartLength2)
AND
StringSub(_Text, _StartIndex, _StartLength, _Str1)
AND
StringSub(_Text, _StartIndex2, _StartLength2, _Str2)
AND
StringConcatenate(_Str2, _Str2, _NewText)
THEN
LLNWIZ_Naming_SetText(_NewText, _Instance);
LLNWIZ_Naming_RecalculateTextLength(_Instance);

PROC
LLNWIZ_Naming_UndoLast((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_Temp_LastVisibleText(_LastText, _Instance)
THEN
LLNWIZ_Naming_SetText(_LastText, _Instance);
LLNWIZ_Naming_RecalculateTextLength(_Instance);

PROC
LLNWIZ_Naming_AppendWord((STRING)_Word, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
StringConcatenate(_Text, _Word, _NextText)
THEN
LLNWIZ_Naming_SetText(_NextText, _Instance);
LLNWIZ_Naming_RecalculateTextLength(_Instance);

PROC
LLNWIZ_Naming_AppendWord((STRING)_Word, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_VisibleText(_,_Instance)
THEN
LLNWIZ_Naming_SetText(_Word, _Instance);
LLNWIZ_Naming_RecalculateTextLength(_Instance);
//END_REGION

//REGION LETTER_CYCLING
QRY
LLNWIZ_Naming_MoveGroupForward((INTEGER)_NextIndex, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_Groups(_NextIndex, _Group)
THEN
LLNWIZ_Naming_ClearCurrentGroup(_Instance);
DB_LLNWIZ_Naming_CurrentGroup(_NextIndex, _Instance);

QRY
LLNWIZ_Naming_MoveGroupForward((INTEGER)_NextIndex, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_Groups(_NextIndex, _)
THEN
LLNWIZ_Naming_ClearCurrentGroup(_Instance);
DB_LLNWIZ_Naming_CurrentGroup(0, _Instance);

QRY
LLNWIZ_Naming_MoveGroupForward((INTEGER)_NextIndex, (INTEGER)_Instance)
THEN
DB_NOOP(1);

QRY
LLNWIZ_Naming_MoveGroupBackward((INTEGER)_NextIndex, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_Groups(_NextIndex, _Group)
THEN
LLNWIZ_Naming_ClearCurrentGroup(_Instance);
DB_LLNWIZ_Naming_CurrentGroup(_NextIndex, _Instance);

QRY
LLNWIZ_Naming_MoveGroupBackward((INTEGER)_NextIndex, (INTEGER)_Instance)
AND
_NextIndex < 0
AND
SysCount("DB_LLNWIZ_Naming_Groups", 2, _TotalGroups)
AND
IntegerSubtract(_TotalGroups, 1, _LastIndex)
THEN
LLNWIZ_Naming_ClearCurrentGroup(_Instance);
DB_LLNWIZ_Naming_CurrentGroup(_LastIndex, _Instance);

QRY
LLNWIZ_Naming_MoveGroupBackward((INTEGER)_NextIndex, (INTEGER)_Instance)
THEN
DB_NOOP(1);

PROC
LLNWIZ_Naming_MoveLetterForward((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_GroupMax(_Group, _Max)
AND
_Index <= _Max
AND
DB_LLNWIZ_Naming_Codex(_Group, _Index, _Letter)
THEN
LLNWIZ_Naming_SetSelectedLetter(_Index, _Group, _Instance);

PROC
LLNWIZ_Naming_MoveLetterForward((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_GroupMax(_Group, _Max)
AND
_Index > _Max
AND
DB_LLNWIZ_Naming_CurrentGroup(_CurrentGroupIndex, _Instance)
AND
IntegerSum(_CurrentGroupIndex, 1, _NextGroupIndex)
AND
LLNWIZ_Naming_MoveGroupForward(_NextGroupIndex, _Instance)
AND
DB_LLNWIZ_Naming_CurrentGroup(_GroupIndex, _Instance)
AND
DB_LLNWIZ_Naming_Groups(_GroupIndex, _NextGroup)
THEN
LLNWIZ_Naming_SetSelectedLetter(0, _NextGroup, _Instance);

PROC
LLNWIZ_Naming_MoveLetterBackward((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
_Index >= 0
AND
DB_LLNWIZ_Naming_Codex(_Group, _Index, _Letter)
THEN
LLNWIZ_Naming_SetSelectedLetter(_Index, _Group, _Instance);

PROC
LLNWIZ_Naming_MoveLetterBackward((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
_Index < 0
AND
DB_LLNWIZ_Naming_CurrentGroup(_CurrentGroupIndex, _Instance)
AND
IntegerSubtract(_CurrentGroupIndex, 1, _NextGroupIndex)
AND
LLNWIZ_Naming_MoveGroupBackward(_NextGroupIndex, _Instance)
AND
DB_LLNWIZ_Naming_CurrentGroup(_GroupIndex, _Instance)
AND
DB_LLNWIZ_Naming_Groups(_GroupIndex, _NextGroup)
AND
DB_LLNWIZ_Naming_GroupMax(_NextGroup, _Max)
THEN
LLNWIZ_Naming_SetSelectedLetter(_Max, _NextGroup, _Instance);
//END_REGION

//REGION LETTER_COUNTING
PROC
LLNWIZ_Naming_CheckMaxLength((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
AND
DB_LLNWIZ_Naming_MaxLength(_Max)
AND
_Length < _Max
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 1)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_LimitReached");

PROC
LLNWIZ_Naming_CheckMaxLength((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
AND
DB_LLNWIZ_Naming_MaxLength(_Max)
AND
_Length >= _Max // Keep it lean, keep it mean
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 0)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_LimitReached");

PROC
LLNWIZ_Naming_FUNC_CharTotalIncrement((STRING)_Text, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_Temp_TextLength(_Text, _Instance, _Length)
AND
IntegerSum(_Length, 1, _NewLength)
THEN
NOT DB_LLNWIZ_Naming_Temp_TextLength(_Text, _Instance, _Length);
DB_LLNWIZ_Naming_Temp_TextLength(_Text, _Instance, _NewLength);

PROC
LLNWIZ_Naming_FUNC_CharTotalIncrement((STRING)_Text, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_Temp_TextLength(_Text, _Instance, _)
THEN
DB_LLNWIZ_Naming_Temp_TextLength(_Text, _Instance, 1);

QRY
LLNWIZ_QRY_Naming_FUNC_CharCount((STRING)_Text, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CharIndex(_Index)
AND
StringSub(_Text, _Index, 1, _Result)
AND
_Result != ""
THEN
LLNWIZ_Naming_FUNC_CharTotalIncrement(_Text, _Instance);

QRY
LLNWIZ_QRY_Naming_FUNC_CharCount((STRING)_Text, (INTEGER)_Instance)
THEN
DB_NOOP(1);

PROC
LLNWIZ_Naming_RecalculateTextLength((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
LLNWIZ_QRY_Naming_FUNC_CharCount(_Text, _Instance)
AND
DB_LLNWIZ_Naming_Temp_TextLength(_Name, _Instance, _Length)
THEN
LLNWIZ_Naming_SetLength(_Length, _Instance);
NOT DB_LLNWIZ_Naming_Temp_TextLength(_Name, _Instance, _Length);
//END_REGION

//REGION NAMING_ACTIONS
PROC
LLNWIZ_Naming_SelectNextLetter((INTEGER)_Amount, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
IntegerSum(_Index, _Amount, _NextIndex)
THEN
LLNWIZ_Naming_MoveLetterForward(_NextIndex, _Group, _Instance);
LLNWIZ_Naming_UpdateSelectedLetterVariable(_Instance);

PROC
LLNWIZ_Naming_SelectPreviousLetter((INTEGER)_Amount, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
IntegerSubtract(_Index, _Amount, _NextIndex)
THEN
LLNWIZ_Naming_MoveLetterBackward(_NextIndex, _Group, _Instance);
LLNWIZ_Naming_UpdateSelectedLetterVariable(_Instance);

PROC
LLNWIZ_Naming_SelectLastLetter((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
DB_LLNWIZ_Naming_GroupMax(_Group, _Max)
THEN
LLNWIZ_Naming_SetSelectedLetter(_Max, _Group, _Instance);
LLNWIZ_Naming_UpdateSelectedLetterVariable(_Instance);

PROC
LLNWIZ_Naming_SelectLastLetter((INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_SelectedLetter(_, _, _Instance)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ:NamingWizard][ERROR] No selected letter set for instance!");

PROC
LLNWIZ_Naming_SelectLastLetter((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
NOT DB_LLNWIZ_Naming_GroupMax(_Group, _)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ:NamingWizard][ERROR] No max amount set for group [",_Group,"]!");

PROC
LLNWIZ_Naming_ChangeCasing((INTEGER)_Instance, "Upper")
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
THEN
LLNWIZ_Naming_SetSelectedLetter(_Index, "Lower", _Instance);

PROC
LLNWIZ_Naming_ChangeCasing((INTEGER)_Instance, "Lower")
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
THEN
LLNWIZ_Naming_SetSelectedLetter(_Index, "Upper", _Instance);

PROC
LLNWIZ_Naming_ChangeCasing((INTEGER)_Instance, (STRING)_Current)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
DB_LLNWIZ_Naming_CurrentCasing(_CurrentGroup, _Instance)
THEN
NOT DB_LLNWIZ_Naming_CurrentCasing(_CurrentGroup, _Instance);
DB_LLNWIZ_Naming_CurrentCasing(_Group, _Instance);

PROC
LLNWIZ_Naming_SetWordState((INTEGER)_Instance, (STRING)_ExtraText)
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
AND
DB_LLNWIZ_Naming_MaxLength(_MaxLength)
AND
IntegertoString(_Length, _LStr)
AND
IntegertoString(_MaxLength, _MStr)
AND
StringConcatenate(_LStr, "/", _Str1)
AND
StringConcatenate(_Str1, _MStr, _Str2)
AND
StringConcatenate("Limit: ", _Str2, _Str3)
AND
StringConcatenate(_Str3, _ExtraText, _Text)
AND
DB_LLNWIZ_Naming_DialogVariables("WordStateText", _VariableName)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableName, _Text);

PROC
LLNWIZ_Naming_SetLength((INTEGER)_Length, (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 0)
THEN
LLNWIZ_Naming_SetWordState(_Instance, "");

PROC
LLNWIZ_Naming_SetLength((INTEGER)_Length, (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 1)
THEN
LLNWIZ_Naming_SetWordState(_Instance, " - Max Character Limit Reached");

PROC
LLNWIZ_Naming_SetFinishedText((STRING)_Text, (CHARACTERGUID)_Player, (INTEGER)_Length)
AND
DB_LLNWIZ_Naming_FinishedText(_LastText, _Player, _LastLength)
THEN
NOT DB_LLNWIZ_Naming_FinishedText(_LastText, _Player, _LastLength);

PROC
LLNWIZ_Naming_SetFinishedText((STRING)_Text, (CHARACTERGUID)_Player, (INTEGER)_Length)
AND
DB_LLNWIZ_Naming_LastFinishedText(_LastText, _Player, _LastLength)
THEN
NOT DB_LLNWIZ_Naming_LastFinishedText(_LastText, _Player, _LastLength);

PROC
LLNWIZ_Naming_SetFinishedText((STRING)_Text, (CHARACTERGUID)_Player, (INTEGER)_Length)
THEN
DB_LLNWIZ_Naming_FinishedText(_Text, _Player, _Length);
DB_LLNWIZ_Naming_LastFinishedText(_Text, _Player, _Length);
//DB_LLNWIZ_Naming_SavedText(_Text, _Player, _Length);
//END_REGION

//REGION FLAG_EVENTS
IF
ObjectFlagSet("LLNWIZ_Naming_AddSpace", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_AddSpace")
THEN
LLNWIZ_Naming_AppendLetter(" ", _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_AddLetter", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_AddLetter")
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
DB_LLNWIZ_Naming_Codex(_Group, _Index, _Letter)
THEN
LLNWIZ_Naming_AppendLetter(_Letter, _Instance);
//DebugBreak("[LLNWIZ] User added letter.");

IF
ObjectFlagSet("LLNWIZ_Naming_RemoveLastLetter", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_RemoveLastLetter")
THEN
LLNWIZ_Naming_RemoveLastLetter(_Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_NextLetter", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_NextLetter")
THEN
LLNWIZ_Naming_SelectNextLetter(1, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_PreviousLetter", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_PreviousLetter")
THEN
LLNWIZ_Naming_SelectPreviousLetter(1, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_NextLetter5", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_NextLetter5")
THEN
LLNWIZ_Naming_SelectNextLetter(5, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_PreviousLetter5", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_PreviousLetter5")
THEN
LLNWIZ_Naming_SelectPreviousLetter(5, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_LetterEnd", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_LetterEnd")
THEN
LLNWIZ_Naming_SelectLastLetter(_Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_LetterStart", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_LetterStart")
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
THEN
LLNWIZ_Naming_SetSelectedLetter(0, _Group, _Instance);
LLNWIZ_Naming_UpdateSelectedLetterVariable(_Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_ChangeCasing", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_ChangeCasing")
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
THEN
LLNWIZ_Naming_ChangeCasing(_Instance, _Group);
LLNWIZ_Naming_UpdateSelectedLetterVariable(_Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_ClearAll", _Speaker, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Speaker, "LLNWIZ_Naming_ClearAll")
THEN
LLNWIZ_Naming_SetText("", _Instance);
LLNWIZ_Naming_SetLength(0, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_RestoreLastText", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_RestoreLastText")
AND
DB_LLNWIZ_Naming_LastFinishedText(_Text, _Player, _Length)
THEN
LLNWIZ_Naming_SetText(_Text, _Instance);
LLNWIZ_Naming_SetLength(_Length, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_CountText", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_CountText")
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
LLNWIZ_QRY_Naming_FUNC_CharCount(_Text, _Instance)
AND
DB_LLNWIZ_Naming_Temp_TextLength(_Name, _Instance, _Length)
THEN
LLNWIZ_Naming_SetLength(_Length, _Instance);
NOT DB_LLNWIZ_Naming_Temp_TextLength(_Name, _Instance, _Length);

IF
ObjectFlagSet("LLNWIZ_Naming_UndoLast", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_UndoLast")
THEN
LLNWIZ_Naming_UndoLast(_Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_RevertToTargetName", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_RevertToTargetName")
THEN
LLNWIZ_Naming_RevertToTargetName(_Player, _Instance);

PROC
LLNWIZ_Naming_RevertToTargetName((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_TargetName(_Player, _Name)
THEN
LLNWIZ_Naming_SetText(_Name, _Instance);
LLNWIZ_Naming_RecalculateTextLength(_Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_SaveText", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_SaveText")
THEN
LLNWIZ_Naming_OnNamingFinished(_Player, _Instance);

IF
ObjectFlagSet("LLNWIZ_Naming_CancelNaming", (CHARACTERGUID)_Player, _Instance)
AND
LLNWIZ_QRY_ClearObjectFlag(_Player, "LLNWIZ_Naming_CancelNaming")
THEN
LLNWIZ_Naming_OnNamingCanceled(_Player, _Instance);

IF
DB_LLNWIZ_Naming_Temp_LastVisibleText((STRING)_LastText, (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_CanUndo", 0)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_CanUndo");

IF
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
DB_LLNWIZ_Naming_Temp_LastVisibleText(_Text, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_CanUndo", 1)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_CanUndo");
//END_REGION

//REGION DIALOG_INITIAL_TEXT
PROC
LLNWIZ_Naming_SetTargetName((STRING)_Name, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_TargetName(_Player, _PreviousName)
THEN
NOT DB_LLNWIZ_Naming_TargetName(_Player, _PreviousName);

PROC
LLNWIZ_Naming_SetTargetName((STRING)_Name, (CHARACTERGUID)_Player)
THEN
DB_LLNWIZ_Naming_TargetName(_Player, _Name);
ObjectSetFlag(_Player, "LLNWIZ_Naming_TargetNameSet");
LLNWIZ_Naming_SetInitialText("", _Player);

PROC
LLNWIZ_Naming_SetTargetCustomName((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName)
THEN
NOT DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName);

PROC
LLNWIZ_Naming_SetTargetCustomName((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
GetVarString(_Target, "LLNWIZ_CustomName", _CustomName)
AND
_CustomName != ""
THEN
DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName);

PROC
LLNWIZ_Naming_SetInitialText((STRING)_Text, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_StartText(_PreviousText, _Player)
THEN
NOT DB_LLNWIZ_Naming_StartText(_Text, _Player);

PROC
LLNWIZ_Naming_SetInitialText((STRING)_Text, (CHARACTERGUID)_Player)
THEN
DB_LLNWIZ_Naming_StartText(_Text, _Player);
//END_REGION

//REGION DIALOG_EVENTS
/*
PROC
LLNWIZ_Naming_InitVariables((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_TextSaved", 1)
AND
GetVarString(_Player, "LLNWIZ_InitialText", _Text)
AND
_Text != ""
THEN
LLNWIZ_Naming_SetInitialText(_Text, _Player);
LeaderLog_Log("DEBUG", "[LLNWIZ:NamingWizard:Main] Found LLNWIZ_InitialText on Player. Setting to initial text.");
*/

PROC
LLNWIZ_Naming_InitVariables((INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_CurrentCasing(_, _Instance)
THEN
DB_LLNWIZ_Naming_CurrentCasing("Upper", _Instance);

PROC
LLNWIZ_Naming_InitVariables((INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_SelectedLetter(_,_,_Instance)
THEN
LLNWIZ_Naming_SetSelectedLetter(0, "Upper", _Instance);
LLNWIZ_Naming_ClearCurrentGroup(_Instance);
DB_LLNWIZ_Naming_CurrentGroup(0, _Instance);
LLNWIZ_Naming_UpdateSelectedLetterVariable(_Instance);
LLNWIZ_Naming_SetLength(0, _Instance);

PROC
LLNWIZ_Naming_InitPlayerVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_TargetName(_Player, _)
AND
LLNWIZ_Naming_QRY_TargetNameVariableSet(_Player)
AND
GetVarString(_Player, "LLNWIZ_TargetName", _Name)
THEN
DB_LLNWIZ_Naming_TargetName(_Player, _Name);

PROC
LLNWIZ_Naming_InitPlayerVariables((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
NOT DB_LLNWIZ_Naming_TargetCustomName(_Player, _)
AND
LLNWIZ_Naming_QRY_CustomNameVariableSet(_Player)
AND
GetVarString(_Player, "LLNWIZ_CustomName", _Name)
THEN
DB_LLNWIZ_Naming_TargetCustomName(_Player, _Name);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
THEN
LLNWIZ_Naming_InitVariables(_Instance);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LLNWIZ_Naming_InitPlayerVariables(_Player, _Instance);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DB_LLNWIZ_Temp_NextWizardGreeting(_Text)
AND
DB_LLNWIZ_Naming_DialogVariables("RandomGreeting", _DialogVar)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _Text);
NOT DB_LLNWIZ_Temp_NextWizardGreeting(_Text);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_LastFinishedText(_Text, _Player, _Length)
AND
DB_LLNWIZ_Naming_DialogVariables("LastConfirmedText", _DialogVar)
THEN
DialogSetVariableStringForInstance(_Instance, _DialogVar, _Text);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_TargetName(_Player, _Name)
AND
DB_LLNWIZ_Naming_DialogVariables("OriginalName", _OriginalNameVar)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_TargetNameSet");
DialogSetVariableStringForInstance(_Instance, _OriginalNameVar, _Name);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_TargetName(_Player, _Name)
AND
DB_LLNWIZ_Naming_DialogVariables("TargetName", _TargetNameVar)
AND
StringConcatenate("Target: ", _Name, _TargetNameVal)
THEN
DialogSetVariableStringForInstance(_Instance, _TargetNameVar, _TargetNameVal);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_TargetName(_Player, _Name)
AND
DB_LLNWIZ_Naming_TargetCustomName(_Player, _CustomName)
AND
DB_LLNWIZ_Naming_DialogVariables("TargetName", _TargetNameVar)
AND
StringConcatenate("Target: ", _CustomName, _Str1)
AND
StringConcatenate(_Str1, " (", _Str2)
AND
StringConcatenate(_Str2, _Name, _Str3)
AND
StringConcatenate(_Str3, ")", _TargetNameVal)
THEN
DialogSetVariableStringForInstance(_Instance, _TargetNameVar, _TargetNameVal);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_StartText(_Text, _Player)
THEN
LLNWIZ_Naming_SetText(_Text, _Instance);
LLNWIZ_Naming_RecalculateTextLength(_Instance);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
NOT DB_LLNWIZ_Naming_StartText(_, _Player)
THEN
LLNWIZ_Naming_SetText("", _Instance);
LLNWIZ_Naming_SetLength(0, _Instance);

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Naming_LastFinishedText(_,_Player,_)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LastNameExists", 0)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_LastNameExists");

PROC
LLNWIZ_Naming_InitMenu((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
NOT DB_LLNWIZ_Naming_LastFinishedText(_,_Player,_)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LastNameExists", 1)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_LastNameExists");

IF
DialogStarted(_Dialog, _Instance)
AND
DB_LLNWIZ_Dialog("NamingWizard", _Dialog)
THEN
LLNWIZ_Naming_InitMenu(_Instance);

IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLNWIZ_Dialog("NamingWizard", _Dialog)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
LLNWIZ_Naming_ClearInstanceVariables(_Instance);
LLNWIZ_Naming_ClearPlayerVariables(_Player);
//END_REGION

//REGION OPEN_ACTIONS
PROC
LLNWIZ_Naming_OpenNamingWizard((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Dialog("NamingWizard", _DialogName)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_WizardPresent");
Proc_StartDialog(0, _DialogName, _Player, _Player);
LLNWIZ_Wizard_SetRandomGreeting(0); // No Wizard

PROC
LLNWIZ_Naming_OpenNamingWizard((CHARACTERGUID)_Player, (CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Dialog("NamingWizard", _DialogName)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_WizardPresent");
Proc_StartDialog(0, _DialogName, _Wizard, _Player);
LLNWIZ_Wizard_SetRandomGreeting(1);
//END_REGION

//REGION LETTER_SETTING
/*
IF
DB_LLNWIZ_Naming_SelectedLetter(_Index, "Letters", _Instance)
AND
DB_LLNWIZ_Naming_CurrentCasing(_ActualGroup, _Instance)
THEN
NOT DB_LLNWIZ_Naming_SelectedLetter(_Index, "Letters", _Instance);
DB_LLNWIZ_Naming_SelectedLetter(_Index, _ActualGroup, _Instance);
*/

PROC
LLNWIZ_Naming_SetSelectedLetter((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
_Group != "Letters"
THEN
LLNWIZ_Naming_ClearSelectedLetter(_Instance);
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance);

PROC
LLNWIZ_Naming_SetSelectedLetter((INTEGER)_Index, "Letters", (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_CurrentCasing(_Group, _Instance)
THEN
LLNWIZ_Naming_ClearSelectedLetter(_Instance);
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance);

PROC
LLNWIZ_Naming_SetSelectedLetter((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
_Group != "Number"
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_GroupIsLetter");

PROC
LLNWIZ_Naming_SetSelectedLetter((INTEGER)_Index, (STRING)_Group, (INTEGER)_Instance)
AND
_Group == "Number"
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_GroupIsLetter");

IF
DB_LLNWIZ_Naming_VisibleText(_NextText, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
_NextText == ""
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_NotEmpty", 1)
THEN
ObjectClearFlag(_Player, "LLNWIZ_Naming_NotEmpty");

IF
DB_LLNWIZ_Naming_VisibleText(_NextText, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
_NextText != ""
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_NotEmpty", 0)
THEN
ObjectSetFlag(_Player, "LLNWIZ_Naming_NotEmpty");

PROC
LLNWIZ_Naming_UpdateSelectedLetterVariable((INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_SelectedLetter(_Index, _Group, _Instance)
AND
DB_LLNWIZ_Naming_Codex(_Group, _Index, _Letter)
AND
DB_LLNWIZ_Naming_DialogVariables("SelectedLetter", _VariableName)
THEN
DialogSetVariableStringForInstance(_Instance, _VariableName, _Letter);
//END_REGION

//REGION FINALIZING
PROC
LLNWIZ_Naming_OnNamingFinished((CHARACTERGUID)_Player, (INTEGER)_Instance)
AND
DB_LLNWIZ_Naming_VisibleText(_Text, _Instance)
AND
_Text != ""
AND
DB_LLNWIZ_Naming_TextLength(_Length, _Instance)
THEN
LLNWIZ_Naming_SetFinishedText(_Text, _Player, _Length);
ObjectSetFlag(_Player, "LLNWIZ_Naming_TextSaved");
SetVarString(_Player, "LLNWIZ_FinalText", _Text);

PROC
LLNWIZ_Naming_OnNamingCanceled((CHARACTERGUID)_Player, (INTEGER)_Instance)
THEN
LLNWIZ_Naming_ClearSessionVariables(_Player);
ObjectSetFlag(_Player, "LLNWIZ_Naming_CancelledSaving");
//SetStoryEvent(_Player, "LLNWIZ_Events_NamingCanceled");
//LeaderLog_Log("DEBUG", "[LLNWIZ:NamingWizard] Player canceled text saving.");
//END_REGION

//REGION RENAMING
PROC
LLNWIZ_Naming_SaveCustomName((STRING)_NewName, (CHARACTERGUID)_Target)
AND
CharacterGetDisplayName(_Target,_,_RealName)
AND
_NewName != _RealName
THEN
SetVarString(_Target, "LLNWIZ_CustomName", _NewName);
ObjectSetFlag(_Target, "LLNWIZ_UsingCustomName");

PROC
LLNWIZ_Naming_SaveCustomName((STRING)_NewName, (CHARACTERGUID)_Target)
AND
CharacterGetDisplayName(_Target,_,_RealName)
AND
_NewName == _RealName
THEN
SetVarString(_Target, "LLNWIZ_CustomName", "");
ObjectClearFlag(_Target, "LLNWIZ_UsingCustomName");

PROC
LLNWIZ_Naming_RenameCharacter((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
GetVarString(_Player, "LLNWIZ_FinalText", _Name)
THEN
CharacterSetCustomName(_Target, _Name);
LLNWIZ_Naming_SaveCustomName(_Name, _Target);
//END_REGION

//REGION LIMIT_REACHED
PROC
LLNWIZ_Naming_AppendWord((STRING)_Word, (INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
AND
ObjectGetFlag(_Player, "LLNWIZ_Naming_LimitReached", 1)
AND
DB_LLNWIZ_Naming_MaxLength(_MaxLength)
AND
DB_LLNWIZ_Naming_VisibleText(_CurrentText, _Instance)
AND
StringSub(_CurrentText, 0, _MaxLength, _NewText)
THEN
LLNWIZ_Naming_SetText(_NewText, _Instance);
LLNWIZ_Naming_SetLength(_MaxLength, _Instance);
LeaderLog_Log("DEBUG", "[LLNWIZ:NamingWizard] Max character limit reached. Trimmed text.");

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
