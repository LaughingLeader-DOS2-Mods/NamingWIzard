Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLNWIZ_Wizard_InitializeSettings();

//Variables
//DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
//DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target)
//DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle)
//DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard)
//DB_LLNWIZ_Wizard_WizardStartTimer(_TimerName, _Wizard, _Player)
//DB_LLNWIZ_Wizard_LastGreeting(_Index)
//DB_LLNWIZ_Temp_NextWizardGreeting(_Text)

//Settings
//DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text)
KBSECTION
//REGION SETTINGS
PROC
LLNWIZ_Wizard_AddGreeting((STRING)_Text)
AND
SysCount("DB_LLNWIZ_Wizard_RandomGreeting", 2, _Index)
THEN
DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text);

PROC
LLNWIZ_Wizard_InitializeSettings()
THEN
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Temp_NextWizardGreeting", 1);
LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Wizard_LastGreeting", 1);

LLNWIZ_System_ClearExistingDatabase("DB_LLNWIZ_Wizard_RandomGreeting", 2);
LLNWIZ_Wizard_AddGreeting("Welcome. What shall we name today?");
LLNWIZ_Wizard_AddGreeting("Beep, boop. I am the Naming Wizard. How may I serv-o you today?");
LLNWIZ_Wizard_AddGreeting("I once had I dream I existed within a moddable user interface. Hah! Can you imagine?");
LLNWIZ_Wizard_AddGreeting("Nameo Whocareseo. Whaddya want?");
LLNWIZ_Wizard_AddGreeting("Renameo Exacteo. Your wish is my command.");
LLNWIZ_Wizard_AddGreeting("Namelolocus Renamicus. Your wish is my command.");
LLNWIZ_Wizard_AddGreeting("Names are power. Let's get crackin'.");
LLNWIZ_Wizard_AddGreeting("Beeeep. This is the Naming Wizard. Press 1 to continue.");
LLNWIZ_Wizard_AddGreeting("Where words fail, music speaks. Unfortunately, words are all I have!");

PROC
LLNWIZ_System_ClearDatabases()
THEN
LLNWIZ_Wizard_DeleteAllWizards();

PROC
LLNWIZ_System_ClearDatabases()
THEN
SysClear("DB_LLNWIZ_Temp_NextWizardGreeting", 1);
SysClear("DB_LLNWIZ_Wizard_CreatedWizard", 2);
SysClear("DB_LLNWIZ_Wizard_WizardTarget", 2);
SysClear("DB_LLNWIZ_Wizard_WizardFX", 2);
SysClear("DB_LLNWIZ_Wizard_WizardDisappearTimer", 2);
SysClear("DB_LLNWIZ_Wizard_WizardStartTimer", 3);
SysClear("DB_LLNWIZ_Wizard_LastGreeting", 1);
SysClear("DB_LLNWIZ_Wizard_RandomGreeting", 2);

PROC
LLNWIZ_Wizard_DeleteAllWizards()
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
THEN
NOT DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player);
LLNWIZ_Wizard_DeleteWizard(_Wizard);
//END_REGION

//REGION GREETING
QRY
LLNWIZ_QRY_Wizard_Internal_RollAgainIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
NOT DB_LLNWIZ_Wizard_LastGreeting(_Index)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_Internal_RollAgainIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
DB_LLNWIZ_Wizard_LastGreeting(_Index)
AND
LLNWIZ_Random(_Max)
THEN
DB_NOOP(1);


QRY
LLNWIZ_QRY_Wizard_Internal_RollIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
NOT DB_LLNWIZ_Wizard_LastGreeting(_Index)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_Internal_RollIfLastGreeting((INTEGER)_Index, (INTEGER)_Max)
AND
DB_LLNWIZ_Wizard_LastGreeting(_Index)
AND
LLNWIZ_Random(_Max)
AND
DB_LLNWIZ_Temp_Rand(_NextIndex)
AND
LLNWIZ_QRY_Wizard_Internal_RollAgainIfLastGreeting(_NextIndex, _Max)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_RandomGreetingIndex()
AND
SysCount("DB_LLNWIZ_Wizard_RandomGreeting", 2, _TotalGreetings)
AND
IntegerSubtract(_TotalGreetings, 1, _MaxIndex)
AND
LLNWIZ_Random(_MaxIndex)
AND
DB_LLNWIZ_Temp_Rand(_Index)
AND
LLNWIZ_QRY_Wizard_Internal_RollIfLastGreeting(_Index, _MaxIndex)
THEN
DB_NOOP(1);

//0 is for no wizards, 1 is for a wizard.
PROC
LLNWIZ_Wizard_SetRandomGreeting(0)
THEN
DB_LLNWIZ_Temp_NextWizardGreeting("Naming Wizard Menu");

PROC
LLNWIZ_Wizard_SetRandomGreeting(1)
AND
SysCount("DB_LLNWIZ_Wizard_RandomGreeting", 2, _TotalGreetings)
AND
IntegerSubtract(_TotalGreetings, 1, _MaxIndex)
AND
LLNWIZ_QRY_Wizard_RandomGreetingIndex()
AND
DB_LLNWIZ_Temp_Rand(_Index)
AND
DB_LLNWIZ_Wizard_RandomGreeting(_Index, _Text)
THEN
DB_LLNWIZ_Temp_NextWizardGreeting(_Text);
LLNWIZ_Wizard_SetLastGreeting(_Index);

PROC
LLNWIZ_Wizard_SetLastGreeting((INTEGER)_Index)
AND
DB_LLNWIZ_Wizard_LastGreeting(_LastIndex)
THEN
NOT DB_LLNWIZ_Wizard_LastGreeting(_LastIndex);

PROC
LLNWIZ_Wizard_SetLastGreeting((INTEGER)_Index)
THEN
DB_LLNWIZ_Wizard_LastGreeting(_Index);

//END_REGION

//REGION HELPERS
PROC
LLNQIZ_Wizard_SetTarget((CHARACTERGUID)_Wizard, (CHARACTERGUID)_Target)
AND
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _LastTarget)
THEN
NOT DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _LastTarget);

PROC
LLNQIZ_Wizard_SetTarget((CHARACTERGUID)_Wizard, (CHARACTERGUID)_Target)
THEN
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target);

//END_REGION

//REGION CREATION
QRY
LLNWIZ_QRY_Wizard_ValidWizardExists((CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
ObjectExists(_Wizard, 1)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_CreateWizard((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
NOT LLNWIZ_QRY_Wizard_ValidWizardExists(_Player)
AND
GetPosition(_Target, _x,_y,_z)
AND
TemporaryCharacterCreateOutOfSightToObject("LLNWIZ_NamingWizard_923ec2b9-f8c0-4fd9-8647-5459f6780b5a", _Target, _Player, 0, "", _Wizard)
THEN
ObjectSetFlag(_Wizard, "LLNWIZ_Wizard_NotRogue");
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player);
LLNQIZ_Wizard_SetTarget(_Wizard, _Target);
TeleportToPosition(_Wizard, _x, _y, _z, "LLNWIZ_Events_WizardCreated", 0, 0);

QRY
LLNWIZ_QRY_Wizard_CreateWizard((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
LLNWIZ_QRY_Wizard_ValidWizardExists(_Player)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
GetPosition(_Target, _x,_y,_z)
THEN
ObjectClearFlag(_Wizard, "LLNWIZ_Wizard_Hiding");
ObjectSetFlag(_Wizard, "LLNWIZ_Wizard_NotRogue");
LLNQIZ_Wizard_SetTarget(_Wizard, _Target);
TeleportToPosition(_Wizard, _x, _y, _z, "LLNWIZ_Events_WizardCreated", 0, 0);
SetOnStage(_Wizard, 1);

PROC
LLWIZ_Wizard_Start((CHARACTERGUID)_Player, (CHARACTERGUID)_Target)
AND
LLNWIZ_QRY_Wizard_CreateWizard(_Player, _Target)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
CharacterGetDisplayName(_Target,_,_TargetName)
THEN
LLNWIZ_Naming_SetTargetName(_TargetName, _Player);
LLNWIZ_Naming_SetTargetCustomName(_Player, _Target);
//END_REGION

//REGION WIZARD_CREATION_EVENTS
IF
StoryEvent((CHARACTERGUID)_Wizard, "LLNWIZ_Events_WizardCreated")
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target)
AND
GetPosition(_Target, _x,_y,_z)
THEN
CharacterMoveToPosition(_Wizard, _x,_y,_z, 1, "LLNWIZ_Events_WizardArrived");
//PlayEffect(_Target, "LLNWIZ_FX_NamingWizard_Book_Teleport_01", "Dummy_OverheadFX");

IF
StoryEvent((CHARACTERGUID)_Wizard, "LLNWIZ_Events_WizardArrived")
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
NOT DB_LLNWIZ_Wizard_WizardStartTimer(_, _Wizard, _Player)
AND
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target)
AND
GetUUID(_Wizard, _ID)
AND
StringConcatenate("LLNWIZ_Timers_Wizard_StartDelay_", _ID, _TimerName)
THEN
CharacterLookAt(_Wizard, _Player);
CharacterFollowCharacter(_Wizard, _Target);
DB_LLNWIZ_Wizard_WizardStartTimer(_TimerName, _Wizard, _Player);
TimerLaunch(_TimerName, 500);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Wizard_WizardStartTimer(_TimerName, _Wizard, _Player)
THEN
NOT DB_LLNWIZ_Wizard_WizardStartTimer(_TimerName, _Wizard, _Player);
LLNWIZ_Naming_OpenNamingWizard(_Player, _Wizard);
//END_REGION

//REGION HIDE_WIZARD
PROC
LLNWIZ_Wizard_HideWizard((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle)
THEN
StopLoopEffect(_Handle);
NOT DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle);

PROC
LLNWIZ_Wizard_HideWizard((CHARACTERGUID)_Wizard)
AND
GetUUID(_Wizard, _ID)
AND
StringConcatenate("LLNWIZ_Timers_Wizard_DisappearDelay_", _ID, _TimerName)
THEN
ObjectSetFlag(_Wizard, "LLNWIZ_Wizard_Disappearing");
DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard);
TimerLaunch(_TimerName, 1000);

PROC
LLNWIZ_Wizard_HideWizard((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target)
THEN
RemoveStatus(_Target, "LLNWIZ_RENAMING");
NOT DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target);

PROC
LLNWIZ_Wizard_HideWizardByPlayer((CHARACTERGUID)_Target)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
THEN
ObjectSetFlag(_Wizard, "LLNWIZ_Wizard_Hiding");
LLNWIZ_Wizard_HideWizard(_Wizard);

PROC
LLNWIZ_Wizard_HideWizardByPlayer((CHARACTERGUID)_Target)
AND
NOT DB_LLNWIZ_Wizard_CreatedWizard(_, _Target)
THEN
LLNWIZ_DebugLog("[Wizard:HideWizardByPlayer] Wizard does not exist for player!");

PROC
LLNWIZ_Wizard_HideWizardByTarget((CHARACTERGUID)_Target)
AND
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target)
THEN
ObjectSetFlag(_Wizard, "LLNWIZ_Wizard_Hiding");
LLNWIZ_Wizard_HideWizard(_Wizard);

PROC
LLNWIZ_Wizard_HideWizardByTarget((CHARACTERGUID)_Target)
AND
NOT DB_LLNWIZ_Wizard_WizardTarget(_, _Target)
THEN
RemoveStatus(_Target, "LLNWIZ_RENAMING");
LLNWIZ_DebugLog("[Wizard:HideWizardByTarget] Wizard does not exist for target!");

PROC
LLNWIZ_Wizard_HideWizardByInstance((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
THEN
ObjectSetFlag(_Wizard, "LLNWIZ_Wizard_Hiding");
LLNWIZ_Wizard_HideWizard(_Wizard);

PROC
LLNWIZ_Wizard_HideWizardByInstance((INTEGER)_Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
NOT DB_LLNWIZ_Wizard_CreatedWizard(_, _Player)
THEN
//LLNWIZ_DebugLog("[Wizard:HideWizardByInstance] Wizard does not exist for player. Skipping.");
DB_NOOP(1);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard)
THEN
ObjectClearFlag(_Wizard, "LLNWIZ_Wizard_Disappearing");
SetOnStage(_Wizard, 0);
NOT DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard);
//END_REGION

//REGION HIDE_EVENTS
IF
DialogEnded(_Dialog, _Instance)
AND
DB_LLNWIZ_MenuDialog(_Dialog)
THEN
LLNWIZ_Wizard_HideWizardByInstance(_Instance);

IF
StoryEvent((CHARACTERGUID)_Wizard, "LLNWIZ_Events_HideWizard")
AND
NOT DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _)
THEN
LLNWIZ_DebugLog("[Wizard][ERROR] Target of event (LLNWIZ_Events_HideWizard) is not a registered naming wizard.");

IF
StoryEvent((CHARACTERGUID)_Wizard, "LLNWIZ_Events_HideWizard")
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
THEN
LLNWIZ_Wizard_HideWizard(_Wizard);
LLNWIZ_DebugLog("[Wizard] Event (LLNWIZ_Events_HideWizard) fired. Hiding wizard.");

//END_REGION

//REGION CLEANUP
PROC
LLNWIZ_Wizard_ClearVariables((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target)
THEN
NOT DB_LLNWIZ_Wizard_WizardTarget(_Wizard, _Target);

PROC
LLNWIZ_Wizard_ClearVariables((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle)
THEN
StopLoopEffect(_Handle);
NOT DB_LLNWIZ_Wizard_WizardFX(_Wizard, _Handle);

PROC
LLNWIZ_Wizard_ClearVariables((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard)
THEN
TimerCancel(_TimerName);
NOT DB_LLNWIZ_Wizard_WizardDisappearTimer(_TimerName, _Wizard);

PROC
LLNWIZ_Wizard_ClearVariables((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_WizardStartTimer(_TimerName, _Wizard, _Player)
THEN
TimerCancel(_TimerName);
NOT DB_LLNWIZ_Wizard_WizardStartTimer(_TimerName, _Wizard, _Player);

PROC
LLNWIZ_Wizard_ClearVariables((CHARACTERGUID)_Wizard)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
THEN
NOT DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player);

PROC
LLNWIZ_Wizard_DeleteWizard((CHARACTERGUID)_Wizard)
AND
ObjectExists(_Wizard, 1)
THEN
SetOnStage(_Wizard, 0);
CharacterDie(_Wizard, 0, "None", _Wizard);

PROC
LLNWIZ_Wizard_DeleteWizard((CHARACTERGUID)_Wizard)
THEN
LLNWIZ_Wizard_ClearVariables(_Wizard);

QRY
LLNWIZ_QRY_Wizard_CleanupStatus((CHARACTERGUID)_Target)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_CleanupStatus((CHARACTERGUID)_Target)
AND
NOT DB_LLNWIZ_Wizard_WizardTarget(_, _Target)
AND
HasActiveStatus(_Target, "LLNWIZ_RENAMING", 1)
THEN
RemoveStatus(_Target, "LLNWIZ_RENAMING");

//END_REGION

//REGION CLEANUP_EVENTS
IF
CharacterSawCharacter(_Char1, _Char2)
AND
LLNWIZ_QRY_Wizard_CleanupStatus(_Char1)
AND
LLNWIZ_QRY_Wizard_CleanupStatus(_Char2)
THEN
DB_NOOP(1);

/*
HasActiveStatus(_Target, "LLNWIZ_RENAMING", 1)
AND
NOT DB_LLNWIZ_Wizard_WizardTarget(_, _Target)
THEN
RemoveStatus(_Target, "LLNWIZ_RENAMING");
*/

IF
GameStarted(_,_)
AND
DB_LLNWIZ_Wizard_CreatedWizard(_Wizard, _Player)
AND
ObjectExists(_Wizard, 0)
THEN
LLNWIZ_Wizard_DeleteWizard(_Wizard);
LLNWIZ_DebugLog("[Wizard] Pruned old wizards.");
//END_REGION

//REGION ROGUE_WIZARDS
QRY
LLNWIZ_QRY_Wizard_CheckRogueFlags((CHARACTERGUID)_Wizard)
AND
ObjectGetFlag(_Wizard, "LLNWIZ_Wizard_NotRogue", 0)
THEN
DB_NOOP(1);

QRY
LLNWIZ_QRY_Wizard_CheckRogueFlags((CHARACTERGUID)_Wizard)
AND
ObjectGetFlag(_Wizard, "LLNWIZ_Wizard_Hiding", 1)
AND
ObjectGetFlag(_Wizard, "LLNWIZ_Wizard_Disappearing", 0)
THEN
DB_NOOP(1);

PROC
LLNWIZ_Wizard_CheckRogue((CHARACTERGUID)_Wizard)
AND
GetTemplate(_Wizard, "LLNWIZ_NamingWizard_923ec2b9-f8c0-4fd9-8647-5459f6780b5a")
AND
NOT DB_LLNWIZ_Wizard_CreatedWizard(_Wizard,_)
AND
LLNWIZ_QRY_Wizard_CheckRogueFlags(_Wizard)
THEN
LLNWIZ_DebugLog("[Wizard][ERROR] Rogue wizard detected (not in the database). Wizard is unregistered. Attempting to remove.");
LLNWIZ_Wizard_DeleteWizard(_Wizard);

IF
CharacterSawCharacter(_Character1, _Character2)
THEN
LLNWIZ_Wizard_CheckRogue(_Character1);
LLNWIZ_Wizard_CheckRogue(_Character2);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
