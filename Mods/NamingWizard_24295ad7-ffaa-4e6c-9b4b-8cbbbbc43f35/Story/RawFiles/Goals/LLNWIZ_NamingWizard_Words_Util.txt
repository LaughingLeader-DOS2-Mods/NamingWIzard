Version 1
SubGoalCombiner SGC_AND
INITSECTION
/*SORTING*/
//DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, _MenuID, _CompletionEvent)
//DB_LLNWIZ_Words_CurrentlySorting(_MenuID)
/**/
//DB_LLNWIZ_Words_ReplacedWords(_Category, _OldWord, _NewWord);
//DB_LLNWIZ_Words_RemovedWords(_Category, _Word);
//DB_LLNWIZ_Temp_IndexRebuilding(_Category, _CurrentIndex, _StartIndex, _Total, _TimerName)

//DB_LLNWIZ_Words_Common(_Index, _Word)			"Common"
//DB_LLNWIZ_Words_Name(_Index, _Name)			"Name"
//DB_LLNWIZ_Words_Saved(_Index, _Word)			"Player"
//DB_LLNWIZ_Words_Mod(_Index, _Word)			"Mod"
//DB_LLNWIZ_Words_CategoryToDatabase(_Category, _Database, _Arity)
KBSECTION
//REGION SETTINGS
PROC
LLNWIZ_System_UpdateDatabases()
THEN
DB_NOOP(1);

PROC
LLNWIZ_System_RegisterDatabases()
THEN
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard_Words_Utils", "DB_LLNWIZ_Temp_IndexRebuilding", 4);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard_Words_Utils", "DB_LLNWIZ_Words_ReplacedWords", 3);
LeaderUpdater_Register_Database("NamingWizard", "LaughingLeader", "NamingWizard_Words_Utils", "DB_LLNWIZ_Words_RemovedWords", 2);
LeaderLog_Log("TRACE", "[LLNWIZ:Words:Utils] Databases registered.");
//END_REGION

//REGION SORTING
PROC
LLNWIZ_Words_SortMenu((STRING)_MenuID)
AND
NOT DB_LLNWIZ_Words_Temp_ActiveSort(_, _MenuID, _)
AND
StringConcatenate("LLNWIZ_Words_SortingMenu_", _MenuID, _DictionaryID)
AND
StringConcatenate("LLNWIZ_Events_OnMenuSortComplete_", _MenuID, _CompletionEvent)
THEN
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, _MenuID, _CompletionEvent);

PROC
LLNWIZ_Words_SortMenu("Common")
AND
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, "Common", _CompletionEvent)
AND
DB_LLNWIZ_Words_Common(_Index, _Word)
THEN
LeaderLib_DictionarySort_AddString(_DictionaryID, "Common", _Word);

PROC
LLNWIZ_Words_SortMenu("Names")
AND
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, "Names", _CompletionEvent)
AND
DB_LLNWIZ_Words_Name(_Index, _Word)
THEN
LeaderLib_DictionarySort_AddString(_DictionaryID, "Names", _Word);

PROC
LLNWIZ_Words_SortMenu("Saved")
AND
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, "Saved", _CompletionEvent)
AND
DB_LLNWIZ_Words_Saved(_Index, _Word)
THEN
LeaderLib_DictionarySort_AddString(_DictionaryID, "Saved", _Word);

PROC
LLNWIZ_Words_SortMenu("Mod")
AND
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, "Mod", _CompletionEvent)
AND
DB_LLNWIZ_Words_Mod(_Index, _Word)
AND
NOT DB_LLLIB_DictionarySort_Temp_StringQueue(_DictionaryID, _, _Word)
THEN
LeaderLib_DictionarySort_AddString(_DictionaryID, "Mod", _Word);
//LeaderLog_Log("DEBUG", "[LLNWIZ:Sorting] Adding [",_Word,"] to sorting queue.");

PROC
LLNWIZ_Words_SortMenu((STRING)_MenuID)
AND
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, _MenuID, _CompletionEvent)
AND
NOT LeaderLib_DictionarySort_QRY_QueueEmpty(_DictionaryID)
THEN
DB_LLNWIZ_Words_CurrentlySorting(_MenuID);
LeaderLib_DictionarySort_StartSorting(_DictionaryID, 1, _CompletionEvent, 0);

IF
StoryEvent(_, _CompletionEvent)
AND
DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, _MenuID, _CompletionEvent)
THEN
NOT DB_LLNWIZ_Words_Temp_ActiveSort(_DictionaryID, _MenuID, _CompletionEvent);
LLNWIZ_Words_OnSortComplete(_DictionaryID, _MenuID);

PROC
LLNWIZ_Words_OnSortComplete((STRING)_DictionaryID, (STRING)_MenuID)
AND
DB_LLNWIZ_WordMenus_RegisteredMenu(_Index, _MenuID, _DisplayName, _DatabaseName, _Arity)
THEN
SysClear(_DatabaseName, _Arity);

PROC
LLNWIZ_Words_OnSortComplete((STRING)_DictionaryID, (STRING)_MenuID)
AND
DB_LLLIB_Array_Iterator(_DictionaryID, _Index)
AND
DB_LLLIB_Dictionary_Data(_DictionaryID, _Index, _MenuID, _Value)
THEN
//LeaderLog_Log("DEBUG", "[LLNWIZ:Sorting] Adding word [",_Value,"] to menu [",_MenuID,"]");
LLNWIZ_Words_AddWordToMenu(_MenuID, _Value);

PROC
LLNWIZ_Words_OnSortComplete((STRING)_DictionaryID, (STRING)_MenuID)
THEN
LeaderLib_Array_ClearDictionary(_DictionaryID);
NOT DB_LLNWIZ_Words_CurrentlySorting(_MenuID);
//END_REGION

//REGION REMOVE_WORD
PROC
LLNWIZ_Words_RemoveAllWords((STRING)_Word)
AND
DB_LLNWIZ_Words_Common(_Index, _Word)
THEN
LLNWIZ_Words_RemoveWord("Common", _Word);

PROC
LLNWIZ_Words_RemoveAllWords((STRING)_Word)
AND
DB_LLNWIZ_Words_Name(_Index, _Word)
THEN
LLNWIZ_Words_RemoveWord("Names", _Word);

PROC
LLNWIZ_Words_RemoveAllWords((STRING)_Word)
AND
DB_LLNWIZ_Words_Saved(_Index, _Word)
THEN
LLNWIZ_Words_RemoveWord("Saved", _Word);

PROC
LLNWIZ_Words_RemoveAllWords((STRING)_Word)
AND
DB_LLNWIZ_Words_Mod(_Index, _Word)
THEN
LLNWIZ_Words_RemoveWord("Mod", _Word);

PROC
LLNWIZ_Words_RemoveWord((STRING)_MenuID, (STRING)_Word)
AND
DB_LLNWIZ_WordMenus_RegisteredMenu(_Index, _MenuID, _DisplayName, _DatabaseName, _Arity)
THEN
LLNWIZ_Words_RemoveFromDatabase(_DatabaseName, _Word, _MenuID);
DB_LLNWIZ_Words_RemovedWords(_MenuID, _Word);

PROC
LLNWIZ_Words_RemoveFromDatabase("DB_LLNWIZ_Words_Common", (STRING)_Word, (STRING)_MenuID)
AND
DB_LLNWIZ_Words_Common(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
NOT DB_LLNWIZ_Words_Common(_Index, _Word);
LeaderLog_Log("DEBUG", "[LLNWIZ:Words:Utils] Removed word [",_Word,"] at index [",_IndexStr,"] from DB_LLNWIZ_Words_Common.");
LLNWIZ_Words_Internal_RebuildIndex(_MenuID, _Index);

PROC
LLNWIZ_Words_RemoveFromDatabase("DB_LLNWIZ_Words_Name", (STRING)_Word, (STRING)_MenuID)
AND
DB_LLNWIZ_Words_Name(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
NOT DB_LLNWIZ_Words_Name(_Index, _Word);
LeaderLog_Log("DEBUG", "[LLNWIZ:Words:Utils] Removed word [",_Word,"] at index [",_IndexStr,"] from DB_LLNWIZ_Words_Name.");
LLNWIZ_Words_Internal_RebuildIndex(_MenuID, _Index);

PROC
LLNWIZ_Words_RemoveFromDatabase("DB_LLNWIZ_Words_Saved", (STRING)_Word, (STRING)_MenuID)
AND
DB_LLNWIZ_Words_Saved(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
NOT DB_LLNWIZ_Words_Saved(_Index, _Word);
LeaderLog_Log("DEBUG", "[LLNWIZ:Words:Utils] Removed word [",_Word,"] at index [",_IndexStr,"] from DB_LLNWIZ_Words_Saved.");
LLNWIZ_Words_Internal_RebuildIndex(_MenuID, _Index);

PROC
LLNWIZ_Words_RemoveFromDatabase("DB_LLNWIZ_Words_Mod", (STRING)_Word, (STRING)_MenuID)
AND
DB_LLNWIZ_Words_Mod(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
NOT DB_LLNWIZ_Words_Mod(_Index, _Word);
LeaderLog_Log("DEBUG", "[LLNWIZ:Words:Utils] Removed word [",_Word,"] at index [",_IndexStr,"] from DB_LLNWIZ_Words_Mod.");
LLNWIZ_Words_Internal_RebuildIndex(_MenuID, _Index);

//END_REGION

//REGION WORD_INDEX_REBUILDING_START
PROC
LLNWIZ_Words_Internal_RebuildIndex((STRING)_MenuID, (INTEGER)_StartIndex)
AND
IntegertoString(_StartIndex, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ:Words:Utils] Rebuilding word index for category [",_MenuID,"], starting at index [",_IndexStr,"]");

PROC
LLNWIZ_Words_Internal_RebuildIndex((STRING)_MenuID, (INTEGER)_StartIndex)
AND
DB_LLNWIZ_WordMenus_RegisteredMenu(_Index, _MenuID, _DisplayName, _DatabaseName, _Arity)
AND
SysCount(_DatabaseName, _Arity, _Total)
AND
LLNWIZ_Random(100)
AND
DB_LLNWIZ_Temp_Rand(_Rand)
AND
IntegertoString(_Rand, _RanStr)
AND
StringConcatenate("LLNWIZ_Timers_IndexRebuildIterator_", _MenuID, _Str1)
AND
StringConcatenate(_Str1, _RanStr, _TimerName)
THEN
DB_LLNWIZ_Temp_IndexRebuilding(_MenuID, _StartIndex, _StartIndex, _Total, _TimerName);
TimerLaunch(_TimerName, 1);
//END_REGION

//REGION WORD_INDEX_REBUILDING
IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Temp_IndexRebuilding("Common", _CurrentIndex, _StartIndex, _Total, _TimerName)
AND
_CurrentIndex < _Total
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
AND
DB_LLNWIZ_Words_Common(_NextIndex, _Word)
AND
IntegertoString(_CurrentIndex, _CurrentIndexStr)
AND
IntegertoString(_NextIndex, _NextIndexStr)
THEN
//LeaderLog_Log("DEBUG", "[LLNWIZ] Set word[",_Word,"] at index ", _NextIndexStr, " to new index of ", _CurrentIndexStr);
NOT DB_LLNWIZ_Words_Common(_NextIndex, _Word);
DB_LLNWIZ_Words_Common(_CurrentIndex, _Word);
NOT DB_LLNWIZ_Temp_IndexRebuilding("Common", _CurrentIndex, _StartIndex, _Total, _TimerName);
DB_LLNWIZ_Temp_IndexRebuilding("Common", _NextIndex, _StartIndex, _Total, _TimerName);
TimerLaunch(_TimerName, 1);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Temp_IndexRebuilding("Names", _CurrentIndex, _StartIndex, _Total, _TimerName)
AND
_CurrentIndex < _Total
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
AND
DB_LLNWIZ_Words_Name(_NextIndex, _Word)
THEN
NOT DB_LLNWIZ_Words_Name(_NextIndex, _Word);
DB_LLNWIZ_Words_Name(_CurrentIndex, _Word);
NOT DB_LLNWIZ_Temp_IndexRebuilding("Names", _CurrentIndex, _StartIndex, _Total, _TimerName);
DB_LLNWIZ_Temp_IndexRebuilding("Names", _NextIndex, _StartIndex, _Total, _TimerName);
TimerLaunch(_TimerName, 1);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Temp_IndexRebuilding("Saved", _CurrentIndex, _StartIndex, _Total, _TimerName)
AND
_CurrentIndex < _Total
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
AND
DB_LLNWIZ_Words_Saved(_NextIndex, _Word)
THEN
NOT DB_LLNWIZ_Words_Saved(_NextIndex, _Word);
DB_LLNWIZ_Words_Saved(_CurrentIndex, _Word);
NOT DB_LLNWIZ_Temp_IndexRebuilding("Saved", _CurrentIndex, _StartIndex, _Total, _TimerName);
DB_LLNWIZ_Temp_IndexRebuilding("Saved", _NextIndex, _StartIndex, _Total, _TimerName);
TimerLaunch(_TimerName, 1);

IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Temp_IndexRebuilding("Mod", _CurrentIndex, _StartIndex, _Total, _TimerName)
AND
_CurrentIndex < _Total
AND
IntegerSum(_CurrentIndex, 1, _NextIndex)
AND
DB_LLNWIZ_Words_Mod(_NextIndex, _Word)
THEN
NOT DB_LLNWIZ_Words_Mod(_NextIndex, _Word);
DB_LLNWIZ_Words_Mod(_CurrentIndex, _Word);
NOT DB_LLNWIZ_Temp_IndexRebuilding("Mod", _CurrentIndex, _StartIndex, _Total, _TimerName);
DB_LLNWIZ_Temp_IndexRebuilding("Mod", _NextIndex, _StartIndex, _Total, _TimerName);
TimerLaunch(_TimerName, 1);
//END_REGION

//REGION INDEX_REBUILDING_END
IF
TimerFinished(_TimerName)
AND
DB_LLNWIZ_Temp_IndexRebuilding(_MenuID, _CurrentIndex, _StartIndex, _Total, _TimerName)
AND
_CurrentIndex >= _Total
THEN
NOT DB_LLNWIZ_Temp_IndexRebuilding(_MenuID, _CurrentIndex, _StartIndex, _Total, _TimerName);
LeaderLog_Log("DEBUG", "[LLNWIZ:Words:Utils] Index rebuilding for word list [",_MenuID,"] complete.");
LLNWIZ_SendStoryEvent("LLNWIZ_Events_WordIndexRebuildingComplete");
//END_REGION

//REGION REPLACE_WORD
PROC
LLNWIZ_Words_ReplaceWord("Common", (STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Common(_Index, _Word)
THEN
NOT DB_LLNWIZ_Words_Common(_Index, _Word);
DB_LLNWIZ_Words_Common(_Index, _NewWord);

PROC
LLNWIZ_Words_ReplaceWord("Names", (STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Name(_Index, _Word)
THEN
NOT DB_LLNWIZ_Words_Name(_Index, _Word);
DB_LLNWIZ_Words_Name(_Index, _NewWord);

PROC
LLNWIZ_Words_ReplaceWord("Saved", (STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Saved(_Index, _Word)
THEN
NOT DB_LLNWIZ_Words_Saved(_Index, _Word);
DB_LLNWIZ_Words_Saved(_Index, _NewWord);

PROC
LLNWIZ_Words_ReplaceWord("Mod", (STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Mod(_Index, _Word)
THEN
NOT DB_LLNWIZ_Words_Mod(_Index, _Word);
DB_LLNWIZ_Words_Mod(_Index, _NewWord);

PROC
LLNWIZ_Words_ReplaceWord((STRING)_MenuID, (STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_WordMenus_RegisteredMenu(_Index, _MenuID, _DisplayName, _DatabaseName, _Arity)
THEN
DB_LLNWIZ_Words_ReplacedWords(_MenuID, _Word, _NewWord);
LeaderLog_Log("DEBUG", "[LLNWIZ][Words:Util] Replaced ",_Word," with ",_NewWord," in menu [",_MenuID,"].");

PROC
LLNWIZ_Words_ReplaceAllWords((STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Common(_Index, _Word)
THEN
LLNWIZ_Words_ReplaceWord("Common", _Word, _NewWord);

PROC
LLNWIZ_Words_ReplaceAllWords((STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Name(_Index, _Word)
THEN
LLNWIZ_Words_ReplaceWord("Names", _Word, _NewWord);

PROC
LLNWIZ_Words_ReplaceAllWords((STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Saved(_Index, _Word)
THEN
LLNWIZ_Words_ReplaceWord("Saved", _Word, _NewWord);

PROC
LLNWIZ_Words_ReplaceAllWords((STRING)_Word, (STRING)_NewWord)
AND
DB_LLNWIZ_Words_Mod(_Index, _Word)
THEN
LLNWIZ_Words_ReplaceWord("Mod", _Word, _NewWord);
//END_REGION

//REGION DEBUG
PROC
LLNWIZ_Debug_PrintWords("All")
AND
DB_LLNWIZ_WordMenus_RegisteredMenu(_Index, _MenuID, _DisplayName, _DatabaseName, _Arity)
THEN
LLNWIZ_Debug_PrintWords(_MenuID);

PROC
LLNWIZ_Debug_PrintWords("Common")
AND
DB_LLNWIZ_Words_Common(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Words:Common][",_IndexStr,"] = ", _Word);

PROC
LLNWIZ_Debug_PrintWords("Names")
AND
DB_LLNWIZ_Words_Name(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Words:Name][",_IndexStr,"] = ", _Word);

PROC
LLNWIZ_Debug_PrintWords("Saved")
AND
DB_LLNWIZ_Words_Saved(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Words:Player][",_IndexStr,"] = ", _Word);

PROC
LLNWIZ_Debug_PrintWords("Mod")
AND
DB_LLNWIZ_Words_Mod(_Index, _Word)
AND
IntegertoString(_Index, _IndexStr)
THEN
LeaderLog_Log("DEBUG", "[LLNWIZ][Words:Mod][",_IndexStr,"] = ", _Word);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_NamingWizard"
